open import Level using () renaming (_Ріћ_ to _Ріћ╦А_)
open import Function using (_$_)

open import Function.Equivalence as FE using ()
open import Relation.Nullary using (yes; no)
open import Relation.Nullary.Decidable using (map)
open import Relation.Nullary.Negation using (contradiction)
open import Relation.Binary using (Setoid; IsEquivalence; Decidable; Tri)
open import Relation.Binary.PropositionalEquality using (_РЅА_) renaming (refl to РЅА-refl; cong to РЅА-cong; setoid to РЅА-setoid)
open Tri

open import Data.Maybe using (nothing)
open import Data.List using ([]; _Рѕи_)
open import Data.Product using (_,_; projРѓЂ)
open import Data.Sum using (injРѓЂ; injРѓѓ)

open import Algebra.Bundles using (CommutativeRing)
open import AKS.Algebra.Bundles using (DecField)

module AKS.Polynomial.Properties {c РёЊ} (F : DecField c РёЊ) where

open import AKS.Nat using (РёЋ; zero; suc; _<_; _РЅЪ_; _РЅЪРЂ║_; _РѕИ_; РёЋРЂ║; РЪЁ_РЄЊРЪє; РЪЁ_РЄЉРЪє) renaming (_+_ to _+РёЋ_)
open import AKS.Nat using (<-cmp; <-РЅц-connex; m+[nРѕИm]РЅАn; РёЋРєњРёЋРЂ║РєњРёЋ; m<nРЄњnРѕИmРЅб0; РЅбРЄњ┬гРЅЪ; <РЄњРЅц)
open import AKS.Nat using (Acc; acc; <-well-founded)

open import Polynomial.Simple.AlmostCommutativeRing using (AlmostCommutativeRing; fromCommutativeRing)
open import Polynomial.Simple.Reflection using (solve; solveOver)

open DecField F
  using (0#; 1#; _+_; _*_; -_; _-_; _РЂ╗┬╣; _/_)
  renaming (Carrier to C)
open DecField F
  using (C/0; _*-nonzero_; _/-nonzero_; -1#-nonzero; 0#РЅЅ1#; *-cancel╦А)
open DecField F using (_РЅѕ_; _РЅЅ_; _РЅѕ?_; setoid)
open Setoid setoid using (refl; sym; trans)
open import Relation.Binary.Reasoning.MultiSetoid
open DecField F using (ring; commutativeRing; *-commutativeMonoid)
open CommutativeRing commutativeRing using (+-assoc; +-comm; +-cong; +-cong╦А; +-cong╩│; +-identity╦А; +-identity╩│; distrib╦А; distrib╩│; -Рђ┐cong; -Рђ┐inverse╦А)
open CommutativeRing commutativeRing using (*-assoc; *-comm; *-cong; *-cong╦А; *-cong╩│; *-identity╦А; *-identity╩│; zero╦А; zero╩│)
open import Algebra.Properties.Ring ring using (-Рђ┐distrib╦А-*)
open import AKS.Exponentiation *-commutativeMonoid using (_^_; _^РЂ║_; ^-homo; ^-^РЂ║-homo; x^nРЅѕx^РЂ║n)

open import AKS.Polynomial.Base F using
  ( Spine; K; _+x^_РѕЎ_; Polynomial; 0рхќ; x^_РѕЎ_; РЪд_РЪД; РЪд_РЪД╦б; _+?_; _РѕЎ­ЮЉІ^_; deg
  ; 1рхќ; _+рхќ_; +рхќ-spine; +рхќ-spine-РЅА-K; +рхќ-spine-РЅА; +рхќ-spine-<; -рхќ_; _-рхќ_; _*рхќ_; *рхќ-spine; _РѕЎрхќ_; РѕЎрхќ-spine
  ; _РЅѕрхќ_; _РЅЅрхќ_; РЅѕРюЊ; _РЅѕРЂ▒_; 0рхќРЅѕ; 0рхќРЅЅ; _РЅѕ╦б_; KРЅѕ; +РЅѕ
  ; РЅѕРЂ▒-refl; РЅѕРЂ▒-sym; РЅѕРЂ▒-trans; РЅѕрхќ-refl; РЅѕрхќ-sym; РЅѕрхќ-trans
  )

open import Algebra.Structures {A = Polynomial} _РЅѕрхќ_ using (IsCommutativeRing; IsRing; IsAbelianGroup; IsGroup; IsMonoid; IsSemigroup; IsMagma)
open import Algebra.Definitions {A = Polynomial} _РЅѕрхќ_ using
  ( _DistributesOver_; _DistributesOver╩│_; _DistributesOver╦А_
  ; RightIdentity; LeftIdentity; Identity; Associative; Commutative
  ; RightInverse; LeftInverse; Inverse; CongruentРѓѓ; CongruentРѓЂ
  )
open import AKS.Algebra.Structures Polynomial _РЅѕрхќ_ using (IsNonZeroCommutativeRing; IsIntegralDomain)

ACR : AlmostCommutativeRing c РёЊ
ACR = fromCommutativeRing commutativeRing (╬╗ _ Рєњ nothing)

foil : Рѕђ a b c d Рєњ (a + b) * (c + d) РЅѕ a * c + a * d + b * c + b * d
foil = solve ACR

РЅѕРЂ▒-isEquivalence : IsEquivalence _РЅѕРЂ▒_
РЅѕРЂ▒-isEquivalence = record
  { refl = РЅѕРЂ▒-refl
  ; sym = РЅѕРЂ▒-sym
  ; trans = РЅѕРЂ▒-trans
  }

РЅѕРЂ▒-setoid : Setoid (c Ріћ╦А РёЊ) (c Ріћ╦А РёЊ)
РЅѕРЂ▒-setoid = record
  { Carrier = Polynomial
  ; _РЅѕ_ = _РЅѕРЂ▒_
  ; isEquivalence = РЅѕРЂ▒-isEquivalence
  }

_РЅѕ╦б?_ : Decidable _РЅѕ╦б_
(K cРѓЂ) РЅѕ╦б? (K cРѓѓ) with projРѓЂ cРѓЂ РЅѕ? projРѓЂ cРѓѓ
... | no ┬гcРѓЂРЅѕcРѓѓ = no ╬╗ { (KРЅѕ cРѓЂРЅѕcРѓѓ) Рєњ contradiction cРѓЂРЅѕcРѓѓ ┬гcРѓЂРЅѕcРѓѓ }
... | yes cРѓЂРЅѕcРѓѓ = yes (KРЅѕ cРѓЂРЅѕcРѓѓ)
(K cРѓЂ) РЅѕ╦б? (cРѓѓ +x^ m РѕЎ q) = no ╬╗ ()
(cРѓЂ +x^ n РѕЎ p) РЅѕ╦б? (K cРѓѓ) = no ╬╗ ()
(cРѓЂ +x^ n РѕЎ p) РЅѕ╦б? (cРѓѓ +x^ m РѕЎ q) with projРѓЂ cРѓЂ РЅѕ? projРѓЂ cРѓѓ
... | no ┬гcРѓЂРЅѕcРѓѓ = no ╬╗ { (+РЅѕ cРѓЂРЅѕcРѓѓ _ _) Рєњ contradiction cРѓЂРЅѕcРѓѓ ┬гcРѓЂРЅѕcРѓѓ }
... | yes cРѓЂРЅѕcРѓѓ with n РЅЪРЂ║ m
...   | no  nРЅбm = no ╬╗ { (+РЅѕ _ nРЅАm _) Рєњ contradiction nРЅАm nРЅбm }
...   | yes nРЅАm with p РЅѕ╦б? q
...     | no ┬гpРЅѕ╦бq = no ╬╗ { (+РЅѕ _ _ pРЅѕ╦бq) Рєњ contradiction pРЅѕ╦бq ┬гpРЅѕ╦бq }
...     | yes pРЅѕ╦бq = yes (+РЅѕ cРѓЂРЅѕcРѓѓ nРЅАm pРЅѕ╦бq)

_РЅѕРЂ▒?_ : Decidable _РЅѕРЂ▒_
0рхќ РЅѕРЂ▒? 0рхќ = yes РЅѕРЂ▒-refl
0рхќ РЅѕРЂ▒? (x^ m РѕЎ q) = no ╬╗ ()
(x^ n РѕЎ p) РЅѕРЂ▒? 0рхќ = no ╬╗ ()
(x^ n РѕЎ p) РЅѕРЂ▒? (x^ m РѕЎ q) with n РЅЪ m
... | no  nРЅбm = no ╬╗ { (0рхќРЅЅ nРЅАm _) Рєњ contradiction nРЅАm nРЅбm }
... | yes nРЅАm with p РЅѕ╦б? q
...   | no ┬гpРЅѕ╦бq = no ╬╗ { (0рхќРЅЅ _ pРЅѕ╦бq) Рєњ contradiction pРЅѕ╦бq ┬гpРЅѕ╦бq }
...   | yes pРЅѕ╦бq = yes (0рхќРЅЅ nРЅАm pРЅѕ╦бq)

РЅѕрхќ-isEquivalence : IsEquivalence _РЅѕрхќ_
РЅѕрхќ-isEquivalence = record
  { refl = РЅѕрхќ-refl
  ; sym = РЅѕрхќ-sym
  ; trans = РЅѕрхќ-trans
  }

РЅѕрхќ-setoid : Setoid (c Ріћ╦А РёЊ) (c Ріћ╦А РёЊ)
РЅѕрхќ-setoid = record
  { Carrier = Polynomial
  ; _РЅѕ_ = _РЅѕрхќ_
  ; isEquivalence = РЅѕрхќ-isEquivalence
  }

РЅѕРЂ▒РЄњРЅѕрхќ : Рѕђ {p q} Рєњ p РЅѕРЂ▒ q Рєњ p РЅѕрхќ q
РЅѕРЂ▒РЄњРЅѕрхќ {0рхќ} {0рхќ} 0рхќРЅѕ = РЅѕрхќ-refl
РЅѕРЂ▒РЄњРЅѕрхќ {x^ n РѕЎ p} {x^ n РѕЎ q} (0рхќРЅЅ РЅА-refl pРЅѕ╦бq) = РЅѕРюЊ ╬╗ x Рєњ *-cong╦А (РЅѕ╦бРЄњРѕђx[pРѓЊРЅѕqРѓЊ] p q pРЅѕ╦бq x)
  where
  РЅѕ╦бРЄњРѕђx[pРѓЊРЅѕqРѓЊ] : Рѕђ p q Рєњ p РЅѕ╦б q Рєњ Рѕђ x Рєњ РЪд p РЪД╦б x РЅѕ РЪд q РЪД╦б x
  РЅѕ╦бРЄњРѕђx[pРѓЊРЅѕqРѓЊ] (K cРѓЂ) (K cРѓѓ) (KРЅѕ cРѓЂРЅѕcРѓѓ) x = cРѓЂРЅѕcРѓѓ
  РЅѕ╦бРЄњРѕђx[pРѓЊРЅѕqРѓЊ] (cРѓЂ +x^ n РѕЎ p) (cРѓѓ +x^ n РѕЎ q) (+РЅѕ cРѓЂРЅѕcРѓѓ РЅА-refl pРЅѕ╦бq) x = beginРЪе setoid РЪЕ
    projРѓЂ cРѓЂ + x ^РЂ║ n * РЪд p РЪД╦б x РЅѕРЪе +-cong cРѓЂРЅѕcРѓѓ (*-cong╦А (РЅѕ╦бРЄњРѕђx[pРѓЊРЅѕqРѓЊ] p q pРЅѕ╦бq x)) РЪЕ
    projРѓЂ cРѓѓ + x ^РЂ║ n * РЪд q РЪД╦б x Рѕј

1рхќ-homo : Рѕђ x Рєњ РЪд 1рхќ РЪД x РЅѕ 1#
1рхќ-homo x = beginРЪе setoid РЪЕ
  1# * 1# РЅѕРЪе *-identity╩│ 1# РЪЕ 1# Рѕј

+рхќ-spine-РЅА-K-homo : Рѕђ n c p x Рєњ РЪд +рхќ-spine-РЅА-K n c p РЪД x РЅѕ x ^ n * (projРѓЂ c + РЪд p РЪД╦б x)
+рхќ-spine-РЅА-K-homo n cРѓЂ (K cРѓѓ) x with projРѓЂ cРѓЂ + projРѓЂ cРѓѓ РЅѕ? 0#
... | yes cРѓЂ+cРѓѓРЅѕ0 = beginРЪе setoid РЪЕ
  0#                            РЅѕРЪе sym (zero╩│ (x ^ n)) РЪЕ
  x ^ n * 0#                    РЅѕРЪе *-cong╦А (sym cРѓЂ+cРѓѓРЅѕ0) РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) Рѕј
... | no  cРѓЂ+cРѓѓРЅЅ0 = refl
+рхќ-spine-РЅА-K-homo n cРѓЂ (cРѓѓ +x^ iРѓѓ РѕЎ p) x with projРѓЂ cРѓЂ + projРѓЂ cРѓѓ РЅѕ? 0#
... | yes cРѓЂ+cРѓѓРЅѕ0 = beginРЪе setoid РЪЕ
  x ^ (n +РёЋ РЪЁ iРѓѓ РЄЊРЪє) * РЪд p РЪД╦б x                       РЅѕРЪе *-cong╩│ (^-^РЂ║-homo x n iРѓѓ) РЪЕ
  (x ^ n * x ^РЂ║ iРѓѓ) * РЪд p РЪД╦б x                         РЅѕРЪе *-assoc (x ^ n) (x ^РЂ║ iРѓѓ) (РЪд p РЪД╦б x) РЪЕ
  x ^ n * (x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x)                         РЅѕРЪе *-cong╦А (sym (+-identity╦А (x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x))) РЪЕ
  x ^ n * (0# + x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x)                    РЅѕРЪе *-cong╦А (+-cong╩│ (sym cРѓЂ+cРѓѓРЅѕ0)) РЪЕ
  x ^ n * ((projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) + x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x) РЅѕРЪе *-cong╦А (+-assoc (projРѓЂ cРѓЂ) (projРѓЂ cРѓѓ) (x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x)) РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + (projРѓЂ cРѓѓ + x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x)) Рѕј
... | no  cРѓЂ+cРѓѓРЅЅ0 = beginРЪе setoid РЪЕ
  x ^ n * ((projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) + x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x) РЅѕРЪе *-cong╦А (+-assoc (projРѓЂ cРѓЂ) (projРѓЂ cРѓѓ) (x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x)) РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + (projРѓЂ cРѓѓ + x ^РЂ║ iРѓѓ * РЪд p РЪД╦б x)) Рѕј


+рхќ-spine-РЅА-homo : Рѕђ n p q x Рєњ РЪд +рхќ-spine-РЅА n p q РЪД x РЅѕ x ^ n * РЪд p РЪД╦б x + x ^ n * РЪд q РЪД╦б x
+рхќ-spine-<-homo : Рѕђ nРѓЂ p nРѓѓ q nРѓЂ<nРѓѓ x Рєњ РЪд +рхќ-spine-< nРѓЂ p nРѓѓ q nРѓЂ<nРѓѓ РЪД x РЅѕ x ^ nРѓЂ * РЪд p РЪД╦б x + x ^ nРѓѓ * РЪд q РЪД╦б x
+рхќ-spine-homo : Рѕђ nРѓЂ p nРѓѓ q x Рєњ РЪд +рхќ-spine nРѓЂ p nРѓѓ q РЪД x РЅѕ x ^ nРѓЂ * РЪд p РЪД╦б x + x ^ nРѓѓ * РЪд q РЪД╦б x

+рхќ-spine-РЅА-lemmaРѓЂ : Рѕђ x a b p q Рєњ x * p + x * q + x * (a + b) РЅѕ x * (a + p) + x * (b + q)
+рхќ-spine-РЅА-lemmaРѓЂ = solve ACR

+рхќ-spine-РЅА-lemmaРѓѓ : Рѕђ x a b p q Рєњ x * a + x * b + x * (p + q) РЅѕ x * (a + p) + x * (b + q)
+рхќ-spine-РЅА-lemmaРѓѓ = solve ACR

+рхќ-spine-РЅА-homo n (K cРѓЂ) q x = beginРЪе setoid РЪЕ
  РЪд +рхќ-spine-РЅА-K n cРѓЂ q РЪД x             РЅѕРЪе +рхќ-spine-РЅА-K-homo n cРѓЂ q x РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + РЪд q РЪД╦б x)         РЅѕРЪе distrib╦А _ _ _ РЪЕ
  x ^ n * projРѓЂ cРѓЂ + (x ^ n) * РЪд q РЪД╦б x Рѕј
+рхќ-spine-РЅА-homo n (cРѓЂ +x^ iРѓЂ РѕЎ p) (K cРѓѓ) x = beginРЪе setoid РЪЕ
  РЪд +рхќ-spine-РЅА-K n cРѓѓ (cРѓЂ +x^ iРѓЂ РѕЎ p) РЪД x         РЅѕРЪе +рхќ-spine-РЅА-K-homo n cРѓѓ (cРѓЂ +x^ iРѓЂ РѕЎ p) x  РЪЕ
  x ^ n * (projРѓЂ cРѓѓ + РЪд cРѓЂ +x^ iРѓЂ РѕЎ p РЪД╦б x)       РЅѕРЪе *-cong╦А (+-comm _ _) РЪЕ
  x ^ n * (РЪд cРѓЂ +x^ iРѓЂ РѕЎ p РЪД╦б x + projРѓЂ cРѓѓ)       РЅѕРЪе distrib╦А _ _ _ РЪЕ
  x ^ n * РЪд cРѓЂ +x^ iРѓЂ РѕЎ p РЪД╦б x + x ^ n * projРѓЂ cРѓѓ Рѕј
+рхќ-spine-РЅА-homo n (cРѓЂ +x^ iРѓЂ РѕЎ p) (cРѓѓ +x^ iРѓѓ РѕЎ q) x with projРѓЂ cРѓЂ + projРѓЂ cРѓѓ РЅѕ? 0#
... | yes cРѓЂ+cРѓѓРЅѕ0 = beginРЪе setoid РЪЕ
  РЪд +рхќ-spine (n +РёЋ РЪЁ iРѓЂ РЄЊРЪє) p (n +РёЋ РЪЁ iРѓѓ РЄЊРЪє) q РЪД x                                           РЅѕРЪе +рхќ-spine-homo (n +РёЋ РЪЁ iРѓЂ РЄЊРЪє) p (n +РёЋ РЪЁ iРѓѓ РЄЊРЪє) q x РЪЕ
  x ^ (n +РёЋ РЪЁ iРѓЂ РЄЊРЪє) * РЪд p РЪД╦б x + x ^ (n +РёЋ РЪЁ iРѓѓ РЄЊРЪє) * РЪд q РЪД╦б x                              РЅѕРЪе sym (+-identity╩│ _) РЪЕ
  x ^ (n +РёЋ РЪЁ iРѓЂ РЄЊРЪє) * РЪд p РЪД╦б x + x ^ (n +РёЋ РЪЁ iРѓѓ РЄЊРЪє) * РЪд q РЪД╦б x + 0#                         РЅѕРЪе +-cong (+-cong (*-cong╩│ (^-^РЂ║-homo x n iРѓЂ)) (*-cong╩│ (^-^РЂ║-homo x n iРѓѓ))) (sym (zero╩│ _)) РЪЕ
  x ^ n * x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x + x ^ n * x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x + x ^ n * 0#                        РЅѕРЪе +-cong (+-cong (*-assoc _ _ _) (*-assoc _ _ _)) (*-cong╦А (sym cРѓЂ+cРѓѓРЅѕ0)) РЪЕ
  x ^ n * (x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ n * (x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x) + x ^ n * (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) РЅѕРЪе +рхќ-spine-РЅА-lemmaРѓЂ _ _ _ _ _ РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ n * (projРѓЂ cРѓѓ + x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x)           Рѕј
... | no  cРѓЂ+cРѓѓРЅЅ0 with +рхќ-spine РЪЁ iРѓЂ РЄЊРЪє p РЪЁ iРѓѓ РЄЊРЪє q | +рхќ-spine-homo РЪЁ iРѓЂ РЄЊРЪє p РЪЁ iРѓѓ РЄЊРЪє q x
...   | 0рхќ | 0РЅѕx^iРѓЂ*p+x^iРѓѓ*q = beginРЪе setoid РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ)                                                                   РЅѕРЪе sym (+-identity╩│ _) РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) + 0#                                                              РЅѕРЪе +-cong (distrib╦А _ _ _) (sym (zero╩│ _)) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * 0#                                                РЅѕРЪе +-cong╦А (*-cong╦А 0РЅѕx^iРѓЂ*p+x^iРѓѓ*q) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ РЪЁ iРѓѓ РЄЊРЪє * РЪд q РЪД╦б x) РЅѕРЪе +-cong╦А (*-cong╦А (+-cong (*-cong╩│ (x^nРЅѕx^РЂ║n x iРѓЂ)) (*-cong╩│ (x^nРЅѕx^РЂ║n x iРѓѓ)))) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * (x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x + x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x)          РЅѕРЪе +рхќ-spine-РЅА-lemmaРѓѓ _ _ _ _ _ РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ n * (projРѓЂ cРѓѓ + x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x)                Рѕј
...   | x^ zero РѕЎ r   | x^0*rРЅѕx^iРѓЂ*p+x^iРѓѓ*q = beginРЪе setoid РЪЕ
  РЪд +рхќ-spine-РЅА-K n (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ , cРѓЂ+cРѓѓРЅЅ0) r РЪД x                                          РЅѕРЪе +рхќ-spine-РЅА-K-homo n (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ , cРѓЂ+cРѓѓРЅЅ0) r x РЪЕ
  x ^ n * ((projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) + РЪд r РЪД╦б x)                                                      РЅѕРЪе distrib╦А _ _ _ РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) + x ^ n * РЪд r РЪД╦б x                                                РЅѕРЪе +-cong (distrib╦А _ _ _) (*-cong╦А (sym (*-identity╦А _))) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * (x ^ zero * РЪд r РЪД╦б x)                             РЅѕРЪе +-cong╦А (*-cong╦А x^0*rРЅѕx^iРѓЂ*p+x^iРѓѓ*q) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ РЪЁ iРѓѓ РЄЊРЪє * РЪд q РЪД╦б x) РЅѕРЪе +-cong╦А (*-cong╦А (+-cong (*-cong╩│ (x^nРЅѕx^РЂ║n x iРѓЂ)) (*-cong╩│ (x^nРЅѕx^РЂ║n x iРѓѓ)))) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * (x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x + x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x)          РЅѕРЪе +рхќ-spine-РЅА-lemmaРѓѓ _ _ _ _ _ РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ n * (projРѓЂ cРѓѓ + x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x)                Рѕј
...   | x^ suc nРѓЃ РѕЎ r | x^nРѓЃ*rРЅѕx^iРѓЂ*p+x^iРѓѓ*q = beginРЪе setoid РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ + x ^РЂ║ РЪЁ suc nРѓЃ РЄЉРЪє * РЪд r РЪД╦б x)                                     РЅѕРЪе distrib╦А _ _ _ РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + projРѓЂ cРѓѓ) + x ^ n * (x ^РЂ║ РЪЁ suc nРѓЃ РЄЉРЪє * РЪд r РЪД╦б x)                           РЅѕРЪе +-cong (distrib╦А _ _ _) (*-cong╦А x^nРѓЃ*rРЅѕx^iРѓЂ*p+x^iРѓѓ*q) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ РЪЁ iРѓѓ РЄЊРЪє * РЪд q РЪД╦б x) РЅѕРЪе +-cong╦А (*-cong╦А (+-cong (*-cong╩│ (x^nРЅѕx^РЂ║n x iРѓЂ)) (*-cong╩│ (x^nРЅѕx^РЂ║n x iРѓѓ)))) РЪЕ
  x ^ n * projРѓЂ cРѓЂ + x ^ n * projРѓЂ cРѓѓ + x ^ n * (x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x + x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x)          РЅѕРЪе +рхќ-spine-РЅА-lemmaРѓѓ _ _ _ _ _ РЪЕ
  x ^ n * (projРѓЂ cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ n * (projРѓЂ cРѓѓ + x ^РЂ║ iРѓѓ * РЪд q РЪД╦б x)                Рѕј


+рхќ-spine-<-lemma
  : Рѕђ nРѓЂ cРѓЂ iРѓЂ p nРѓѓ q (nРѓЂ<nРѓѓ : nРѓЂ < nРѓѓ) x
  Рєњ x ^ nРѓЂ * (cРѓЂ + (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ (nРѓѓ РѕИ nРѓЂ) * РЪд q РЪД╦б x))
  РЅѕ x ^ nРѓЂ * (cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ nРѓѓ * РЪд q РЪД╦б x
+рхќ-spine-<-lemma nРѓЂ cРѓЂ iРѓЂ p nРѓѓ q nРѓЂ<nРѓѓ x = beginРЪе setoid РЪЕ
  x ^ nРѓЂ * (cРѓЂ + (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ (nРѓѓ РѕИ nРѓЂ) * РЪд q РЪД╦б x))          РЅѕРЪе *-cong╦А (sym (+-assoc _ _ _)) РЪЕ
  x ^ nРѓЂ * ((cРѓЂ + x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x) + x ^ (nРѓѓ РѕИ nРѓЂ) * РЪд q РЪД╦б x)          РЅѕРЪе distrib╦А _ _ _ РЪЕ
  x ^ nРѓЂ * (cРѓЂ + x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x) + x ^ nРѓЂ * (x ^ (nРѓѓ РѕИ nРѓЂ) * РЪд q РЪД╦б x) РЅѕРЪе +-cong (*-cong╦А (+-cong╦А (*-cong╩│ (x^nРЅѕx^РЂ║n x iРѓЂ)))) (sym (*-assoc _ _ _)) РЪЕ
  x ^ nРѓЂ * (cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + (x ^ nРѓЂ * x ^ (nРѓѓ РѕИ nРѓЂ)) * РЪд q РЪД╦б x     РЅѕРЪе +-cong╦А (*-cong╩│ (sym (^-homo x nРѓЂ (nРѓѓ РѕИ nРѓЂ)))) РЪЕ
  x ^ nРѓЂ * (cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ (nРѓЂ +РёЋ (nРѓѓ РѕИ nРѓЂ)) * РЪд q РЪД╦б x        РЅАРЪе РЅА-cong (╬╗ t Рєњ x ^ nРѓЂ * (cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ t * РЪд q РЪД╦б x) (m+[nРѕИm]РЅАn (<РЄњРЅц nРѓЂ<nРѓѓ)) РЪЕ
  x ^ nРѓЂ * (cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ nРѓѓ * РЪд q РЪД╦б x                       Рѕј

+рхќ-spine-<-homo nРѓЂ (K cРѓЂ) nРѓѓ q nРѓЂ<nРѓѓ x = beginРЪе setoid РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + x ^РЂ║ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє * РЪд q РЪД╦б x)          РЅѕРЪе distrib╦А (x ^ nРѓЂ) (projРѓЂ cРѓЂ) (x ^РЂ║ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє * РЪд q РЪД╦б x) РЪЕ
  x ^ nРѓЂ * projРѓЂ cРѓЂ + x ^ nРѓЂ * (x ^РЂ║ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє * РЪд q РЪД╦б x) РЅѕРЪе +-cong╦А (sym (*-assoc (x ^ nРѓЂ) (x ^РЂ║ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє) (РЪд q РЪД╦б x))) РЪЕ
  x ^ nРѓЂ * projРѓЂ cРѓЂ + (x ^ nРѓЂ * x ^РЂ║ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє) * РЪд q РЪД╦б x РЅѕРЪе +-cong╦А (*-cong╩│ (sym (^-^РЂ║-homo x nРѓЂ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє))) РЪЕ
  x ^ nРѓЂ * projРѓЂ cРѓЂ + x ^ (nРѓЂ +РёЋ РЪЁ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє РЄЊРЪє) * РЪд q РЪД╦б x РЅАРЪе РЅА-cong (╬╗ t Рєњ x ^ nРѓЂ * projРѓЂ cРѓЂ + x ^ t * РЪд q РЪД╦б x) lemma РЪЕ
  x ^ nРѓЂ * projРѓЂ cРѓЂ + x ^ nРѓѓ * РЪд q РЪД╦б x Рѕј
  where
  lemma : nРѓЂ +РёЋ РЪЁ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє РЄЊРЪє РЅА nРѓѓ
  lemma = beginРЪе РЅА-setoid РёЋ РЪЕ
    nРѓЂ +РёЋ РЪЁ РЪЁ nРѓѓ РѕИ nРѓЂ РЄЉРЪє РЄЊРЪє РЅАРЪе РЅА-cong (╬╗ x Рєњ nРѓЂ +РёЋ x) (РёЋРєњРёЋРЂ║РєњРёЋ (nРѓѓ РѕИ nРѓЂ) {РЅбРЄњ┬гРЅЪ (m<nРЄњnРѕИmРЅб0 nРѓЂ<nРѓѓ)}) РЪЕ
    nРѓЂ +РёЋ (nРѓѓ РѕИ nРѓЂ)         РЅАРЪе m+[nРѕИm]РЅАn {nРѓЂ} {nРѓѓ} (<РЄњРЅц nРѓЂ<nРѓѓ) РЪЕ
    nРѓѓ Рѕј
+рхќ-spine-<-homo nРѓЂ (cРѓЂ +x^ iРѓЂ РѕЎ p) nРѓѓ q nРѓЂ<nРѓѓ x with +рхќ-spine РЪЁ iРѓЂ РЄЊРЪє p (nРѓѓ РѕИ nРѓЂ) q | +рхќ-spine-homo РЪЁ iРѓЂ РЄЊРЪє p (nРѓѓ РѕИ nРѓЂ) q x
... | 0рхќ | 0РЅѕx^iРѓЂ*p+x^[nРѓѓРѕИnРѓЂ]*q = beginРЪе setoid РЪЕ
  x ^ nРѓЂ * projРѓЂ cРѓЂ                                                         РЅѕРЪе *-cong╦А (sym (+-identity╩│ (projРѓЂ cРѓЂ))) РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + 0#)                                                  РЅѕРЪе *-cong╦А (+-cong╦А 0РЅѕx^iРѓЂ*p+x^[nРѓѓРѕИnРѓЂ]*q) РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ (nРѓѓ РѕИ nРѓЂ) * РЪд q РЪД╦б x)) РЅѕРЪе +рхќ-spine-<-lemma nРѓЂ (projРѓЂ cРѓЂ) iРѓЂ p nРѓѓ q nРѓЂ<nРѓѓ x РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ nРѓѓ * РЪд q РЪД╦б x              Рѕј
... | x^ zero   РѕЎ r | x^0*rРЅѕx^iРѓЂ*p+x^[nРѓѓРѕИnРѓЂ]*q = beginРЪе setoid РЪЕ
  РЪд +рхќ-spine-РЅА-K nРѓЂ cРѓЂ r РЪД x                                                РЅѕРЪе +рхќ-spine-РЅА-K-homo nРѓЂ cРѓЂ r x РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + РЪд r РЪД╦б x)                                            РЅѕРЪе *-cong╦А (+-cong╦А (sym (*-identity╦А (РЪд r РЪД╦б x)))) РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + x ^ zero * РЪд r РЪД╦б x)                                 РЅѕРЪе *-cong╦А (+-cong╦А x^0*rРЅѕx^iРѓЂ*p+x^[nРѓѓРѕИnРѓЂ]*q) РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ (nРѓѓ РѕИ nРѓЂ) * РЪд q РЪД╦б x)) РЅѕРЪе +рхќ-spine-<-lemma nРѓЂ (projРѓЂ cРѓЂ) iРѓЂ p nРѓѓ q nРѓЂ<nРѓѓ x РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ nРѓѓ * РЪд q РЪД╦б x              Рѕј
... | x^ suc nРѓЃ РѕЎ r | x^nРѓЃ*rРЅѕx^iРѓЂ*p+x^[nРѓѓРѕИnРѓЂ]*q = beginРЪе setoid РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + x ^РЂ║ РЪЁ suc nРѓЃ РЄЉРЪє * РЪд r РЪД╦б x)                         РЅѕРЪе *-cong╦А (+-cong╦А x^nРѓЃ*rРЅѕx^iРѓЂ*p+x^[nРѓѓРѕИnРѓЂ]*q) РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + (x ^ РЪЁ iРѓЂ РЄЊРЪє * РЪд p РЪД╦б x + x ^ (nРѓѓ РѕИ nРѓЂ) * РЪд q РЪД╦б x)) РЅѕРЪе +рхќ-spine-<-lemma nРѓЂ (projРѓЂ cРѓЂ) iРѓЂ p nРѓѓ q nРѓЂ<nРѓѓ x РЪЕ
  x ^ nРѓЂ * (projРѓЂ cРѓЂ + x ^РЂ║ iРѓЂ * РЪд p РЪД╦б x) + x ^ nРѓѓ * РЪд q РЪД╦б x              Рѕј

+рхќ-spine-homo nРѓЂ p nРѓѓ q x with <-cmp nРѓЂ nРѓѓ
... | tri< nРѓЂ<nРѓѓ _ _  = +рхќ-spine-<-homo nРѓЂ p nРѓѓ q nРѓЂ<nРѓѓ x
... | triРЅѕ _ РЅА-refl _ = +рхќ-spine-РЅА-homo nРѓЂ p q x
... | tri> _ _ nРѓЂ>nРѓѓ  = beginРЪе setoid РЪЕ
  РЪд +рхќ-spine-< nРѓѓ q nРѓЂ p nРѓЂ>nРѓѓ РЪД x      РЅѕРЪе +рхќ-spine-<-homo nРѓѓ q nРѓЂ p nРѓЂ>nРѓѓ x РЪЕ
  x ^ nРѓѓ * РЪд q РЪД╦б x + x ^ nРѓЂ * РЪд p РЪД╦б x РЅѕРЪе +-comm (x ^ nРѓѓ * РЪд q РЪД╦б x) (x ^ nРѓЂ * РЪд p РЪД╦б x) РЪЕ
  x ^ nРѓЂ * РЪд p РЪД╦б x + x ^ nРѓѓ * РЪд q РЪД╦б x Рѕј

+рхќ-homo : Рѕђ p q x Рєњ РЪд p +рхќ q РЪД x РЅѕ РЪд p РЪД x + РЪд q РЪД x
+рхќ-homo 0рхќ q x = sym (+-identity╦А (РЪд q РЪД x))
+рхќ-homo (x^ oРѓЂ РѕЎ p) 0рхќ x = sym (+-identity╩│ (РЪд x^ oРѓЂ РѕЎ p РЪД x))
+рхќ-homo (x^ oРѓЂ РѕЎ p) (x^ oРѓѓ РѕЎ q) x = +рхќ-spine-homo oРѓЂ p oРѓѓ q x

РѕЎрхќ-spine-homo : Рѕђ a p x Рєњ РЪд РѕЎрхќ-spine a p РЪД╦б x РЅѕ projРѓЂ a * РЪд p РЪД╦б x
РѕЎрхќ-spine-homo a (K c) x = refl
РѕЎрхќ-spine-homo a (c +x^ m РѕЎ p) x = beginРЪе setoid РЪЕ
  projРѓЂ a * projРѓЂ c + (x ^РЂ║ m) * РЪд РѕЎрхќ-spine a p РЪД╦б x  РЅѕРЪе +-cong╦А (*-cong╦А (РѕЎрхќ-spine-homo a p x)) РЪЕ
  projРѓЂ a * projРѓЂ c + (x ^РЂ║ m) * (projРѓЂ a * РЪд p РЪД╦б x) РЅѕРЪе +-cong╦А (x*[y*z]РЅѕy*[x*z] (x ^РЂ║ m) (projРѓЂ a) (РЪд p РЪД╦б x)) РЪЕ
  projРѓЂ a * projРѓЂ c + projРѓЂ a * (x ^РЂ║ m * РЪд p РЪД╦б x)   РЅѕРЪе sym (distrib╦А (projРѓЂ a) (projРѓЂ c) ((x ^РЂ║ m) * РЪд p РЪД╦б x)) РЪЕ
  projРѓЂ a * (projРѓЂ c + (x ^РЂ║ m) * РЪд p РЪД╦б x)           Рѕј
  where
  x*[y*z]РЅѕy*[x*z] : Рѕђ x y z Рєњ x * (y * z) РЅѕ y * (x * z)
  x*[y*z]РЅѕy*[x*z] = solve ACR

РѕЎрхќ-homo : Рѕђ a p x Рєњ РЪд a РѕЎрхќ p РЪД x РЅѕ projРѓЂ a * РЪд p РЪД x
РѕЎрхќ-homo a 0рхќ x = sym (zero╩│ (projРѓЂ a))
РѕЎрхќ-homo a (x^ n РѕЎ p) x = beginРЪе setoid РЪЕ
  x ^ n * РЪд РѕЎрхќ-spine a p РЪД╦б x  РЅѕРЪе *-cong╦А (РѕЎрхќ-spine-homo a p x) РЪЕ
  x ^ n * (projРѓЂ a * РЪд p РЪД╦б x) РЅѕРЪе sym (*-assoc (x ^ n) (projРѓЂ a) (РЪд p РЪД╦б x)) РЪЕ
  (x ^ n * projРѓЂ a) * РЪд p РЪД╦б x РЅѕРЪе *-cong╩│ (*-comm (x ^ n) (projРѓЂ a)) РЪЕ
  (projРѓЂ a * x ^ n) * РЪд p РЪД╦б x РЅѕРЪе *-assoc (projРѓЂ a) (x ^ n) (РЪд p РЪД╦б x) РЪЕ
  projРѓЂ a * (x ^ n * РЪд p РЪД╦б x) Рѕј

*рхќ-homo : Рѕђ p q x Рєњ РЪд p *рхќ q РЪД x РЅѕ РЪд p РЪД x * РЪд q РЪД x
*рхќ-homo 0рхќ q x = sym (zero╦А (РЪд q РЪД x))
*рхќ-homo (x^ oРѓЂ РѕЎ p) 0рхќ x = sym (zero╩│ (x ^ oРѓЂ * РЪд p РЪД╦б x))
*рхќ-homo (x^ oРѓЂ РѕЎ p) (x^ oРѓѓ РѕЎ q) x = *рхќ-spine-homo oРѓЂ p oРѓѓ q
  where
  final : Рѕђ x y z w Рєњ (x * y) * (z * w) РЅѕ (x * z) * (y * w)
  final = solve ACR
  lemmaРѓЂ : Рѕђ a b d k q Рєњ a * b * d * (k * q) РЅѕ a * b * (k * (d * q))
  lemmaРѓЂ a b d k q =  beginРЪе setoid РЪЕ
    a * b * d * (k * q)   РЅѕРЪе *-assoc (a * b) d (k * q) РЪЕ
    a * b * (d * (k * q)) РЅѕРЪе *-cong╦А (sym (*-assoc d k q)) РЪЕ
    a * b * ((d * k) * q) РЅѕРЪе *-cong╦А (*-cong╩│ (*-comm d k)) РЪЕ
    a * b * ((k * d) * q) РЅѕРЪе *-cong╦А (*-assoc k d q) РЪЕ
    a * b * (k * (d * q)) Рѕј
  lemmaРѓѓ : Рѕђ o c l p Рєњ o * c * (l * p) РЅѕ o * (c * p * l)
  lemmaРѓѓ o c l p = beginРЪе setoid РЪЕ
    o * c * (l * p)   РЅѕРЪе *-assoc o c (l * p) РЪЕ
    o * (c * (l * p)) РЅѕРЪе *-cong╦А (*-cong╦А (*-comm l p)) РЪЕ
    o * (c * (p * l)) РЅѕРЪе *-cong╦А (sym (*-assoc c p l)) РЪЕ
    o * ((c * p) * l) Рѕј
  lemmaРѓЃ : Рѕђ a c p b d q Рєњ a * c * p * (b * d * q) РЅѕ a * b * (c * p * (d * q))
  lemmaРѓЃ a c p b d q = beginРЪе setoid РЪЕ
    (a * c) * p * ((b * d) * q) РЅѕРЪе *-cong (*-assoc a c p) (*-assoc b d q) РЪЕ
    a * (c * p) * (b * (d * q)) РЅѕРЪе final a (c * p) b (d * q) РЪЕ
    a * b * (c * p * (d * q)) Рѕј
  mult
    : Рѕђ a b c d k l p q
    Рєњ (a * b) * (k * l)
    + (a * b * d) * (k * q)
    + (a * b * c) * (l * p)
    + (a * c * p) * (b * d * q)
    РЅѕ (a * (k + c * p)) * (b * (l + d * q))
  mult a b c d k l p q = beginРЪе setoid РЪЕ
    (a * b) * (k * l) + (a * b * d) * (k * q) +
    (a * b * c) * (l * p) + (a * c * p) * (b * d * q)
    РЅѕРЪе +-cong (+-cong (+-cong╦А (lemmaРѓЂ a b d k q))
                      (lemmaРѓѓ (a * b) c l p))
              (lemmaРѓЃ a c p b d q)
     РЪЕ
    (a * b) * (k * l) + (a * b) * (k * (d * q)) +
    (a * b) * ((c * p) * l) + (a * b) * ((c * p) * (d * q))
    РЅѕРЪе +-assoc _ _ _ РЪЕ
    ((a * b) * (k * l) + (a * b) * (k * (d * q))) +
    ((a * b) * ((c * p) * l) + (a * b) * ((c * p) * (d * q)))
    РЅѕРЪе +-cong (sym (distrib╦А (a * b) (k * l) (k * (d * q))))
              (sym (distrib╦А (a * b) (c * p * l) ((c * p) * (d * q))))
     РЪЕ
    (a * b) * (k * l + k * (d * q)) +
    (a * b) * ((c * p) * l + (c * p) * (d * q))
    РЅѕРЪе sym (distrib╦А (a * b) _ _) РЪЕ
    (a * b) * ((k * l + k * (d * q)) + ((c * p) * l + (c * p) * (d * q)))
    РЅѕРЪе *-cong╦А (sym (+-assoc _ _ _)) РЪЕ
    (a * b) * (k * l + k * (d * q) + (c * p) * l + (c * p) * (d * q))
    РЅѕРЪе *-cong╦А (sym (foil k (c * p) l (d * q))) РЪЕ
    (a * b) * ((k + c * p) * (l + d * q))
    РЅѕРЪе final a b (k + c * p) (l + d * q) РЪЕ
    (a * (k + c * p)) * (b * (l + d * q)) Рѕј

  *рхќ-spine-homo : Рѕђ oРѓЂ p oРѓѓ q Рєњ РЪд *рхќ-spine oРѓЂ p oРѓѓ q РЪД x РЅѕ (x ^ oРѓЂ * РЪд p РЪД╦б x) * (x ^ oРѓѓ * РЪд q РЪД╦б x)
  *рхќ-spine-homo oРѓЂ (K cРѓЂ) oРѓѓ q = beginРЪе setoid РЪЕ
    x ^ (oРѓЂ +РёЋ oРѓѓ) * РЪд РѕЎрхќ-spine cРѓЂ q РЪД╦б x     РЅѕРЪе *-cong╦А (РѕЎрхќ-spine-homo cРѓЂ q x) РЪЕ
    x ^ (oРѓЂ +РёЋ oРѓѓ) * (projРѓЂ cРѓЂ * РЪд q РЪД╦б x)    РЅѕРЪе *-cong╩│ (^-homo x oРѓЂ oРѓѓ) РЪЕ
    (x ^ oРѓЂ * x ^ oРѓѓ) * (projРѓЂ cРѓЂ * РЪд q РЪД╦б x) РЅѕРЪе final (x ^ oРѓЂ) (x ^ oРѓѓ) (projРѓЂ cРѓЂ) (РЪд q РЪД╦б x)  РЪЕ
    (x ^ oРѓЂ * projРѓЂ cРѓЂ) * (x ^ oРѓѓ * РЪд q РЪД╦б x) Рѕј
  *рхќ-spine-homo oРѓЂ (cРѓЂ +x^ nРѓЂ РѕЎ p) oРѓѓ (K cРѓѓ) = beginРЪе setoid РЪЕ
    x ^ (oРѓЂ +РёЋ oРѓѓ) * (projРѓЂ cРѓѓ * projРѓЂ cРѓЂ + (x ^РЂ║ nРѓЂ) * РЪд РѕЎрхќ-spine cРѓѓ p РЪД╦б x)     РЅѕРЪе *-cong (^-homo x oРѓЂ oРѓѓ) (+-cong (*-comm (projРѓЂ cРѓѓ) (projРѓЂ cРѓЂ)) (*-cong╦А (РѕЎрхќ-spine-homo cРѓѓ p x))) РЪЕ
    (x ^ oРѓЂ * x ^ oРѓѓ) * (projРѓЂ cРѓЂ * projРѓЂ cРѓѓ + (x ^РЂ║ nРѓЂ) * (projРѓЂ cРѓѓ * РЪд p РЪД╦б x)) РЅѕРЪе *-cong╦А (+-cong╦А (*-cong╦А (*-comm (projРѓЂ cРѓѓ) (РЪд p РЪД╦б x)))) РЪЕ
    (x ^ oРѓЂ * x ^ oРѓѓ) * (projРѓЂ cРѓЂ * projРѓЂ cРѓѓ + (x ^РЂ║ nРѓЂ) * (РЪд p РЪД╦б x * projРѓЂ cРѓѓ)) РЅѕРЪе *-cong╦А (+-cong╦А (sym (*-assoc (x ^РЂ║ nРѓЂ) (РЪд p РЪД╦б x) (projРѓЂ cРѓѓ)))) РЪЕ
    (x ^ oРѓЂ * x ^ oРѓѓ) * (projРѓЂ cРѓЂ * projРѓЂ cРѓѓ + (x ^РЂ║ nРѓЂ * РЪд p РЪД╦б x) * projРѓЂ cРѓѓ)   РЅѕРЪе *-cong╦А (sym (distrib╩│ (projРѓЂ cРѓѓ) (projРѓЂ cРѓЂ) (x ^РЂ║ nРѓЂ * РЪд p РЪД╦б x))) РЪЕ
    (x ^ oРѓЂ * x ^ oРѓѓ) * ((projРѓЂ cРѓЂ + (x ^РЂ║ nРѓЂ * РЪд p РЪД╦б x)) * projРѓЂ cРѓѓ)            РЅѕРЪе final (x ^ oРѓЂ) (x ^ oРѓѓ) (projРѓЂ cРѓЂ + (x ^РЂ║ nРѓЂ * РЪд p РЪД╦б x)) (projРѓЂ cРѓѓ) РЪЕ
    x ^ oРѓЂ * (projРѓЂ cРѓЂ + (x ^РЂ║ nРѓЂ) * РЪд p РЪД╦б x) * (x ^ oРѓѓ * projРѓЂ cРѓѓ)              Рѕј
  *рхќ-spine-homo oРѓЂ (cРѓЂ +x^ nРѓЂ РѕЎ p) oРѓѓ (cРѓѓ +x^ nРѓѓ РѕЎ q) = beginРЪе setoid РЪЕ
    РЪд x^ (oРѓЂ +РёЋ oРѓѓ) РѕЎ K (cРѓЂ *-nonzero cРѓѓ)
    +рхќ cРѓЂ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) РѕЎ q
    +рхќ cРѓѓ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) РѕЎ p
    +рхќ *рхќ-spine (oРѓЂ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) p (oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) q РЪД x
    РЅѕРЪе +рхќ-homo (x^ (oРѓЂ +РёЋ oРѓѓ) РѕЎ K (cРѓЂ *-nonzero cРѓѓ) +рхќ
                cРѓЂ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) РѕЎ q +рхќ
                cРѓѓ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) РѕЎ p)
               (*рхќ-spine (oРѓЂ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) p (oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) q) x
     РЪЕ
    РЪд x^ (oРѓЂ +РёЋ oРѓѓ) РѕЎ K (cРѓЂ *-nonzero cРѓѓ)
    +рхќ cРѓЂ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) РѕЎ q
    +рхќ cРѓѓ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) РѕЎ p РЪД x
    + РЪд *рхќ-spine (oРѓЂ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) p (oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) q РЪД x
    РЅѕРЪе +-cong (+рхќ-homo (x^ (oРѓЂ +РёЋ oРѓѓ) РѕЎ K (cРѓЂ *-nonzero cРѓѓ) +рхќ
                        cРѓЂ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) РѕЎ q)
                       (cРѓѓ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) РѕЎ p) x)
              (*рхќ-spine-homo (oРѓЂ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) p (oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) q)
     РЪЕ
    РЪд x^ (oРѓЂ +РёЋ oРѓѓ) РѕЎ K (cРѓЂ *-nonzero cРѓѓ)
    +рхќ cРѓЂ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) РѕЎ q РЪД x
    + РЪд cРѓѓ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) РѕЎ p РЪД x
    + (x ^ (oРѓЂ +РёЋ РЪЁ nРѓЂ РЄЊРЪє) * РЪд p РЪД╦б x) * (x ^ (oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) * РЪд q РЪД╦б x)
    РЅѕРЪе +-cong (+-cong (+рхќ-homo (x^ (oРѓЂ +РёЋ oРѓѓ) РѕЎ K (cРѓЂ *-nonzero cРѓѓ))
                               (cРѓЂ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) РѕЎ q) x)
                      (*-cong  (^-^РЂ║-homo x (oРѓЂ +РёЋ oРѓѓ) nРѓЂ)
                               (РѕЎрхќ-spine-homo cРѓѓ p x)))
              (*-cong (*-cong╩│ (^-^РЂ║-homo x oРѓЂ nРѓЂ))
                      (*-cong╩│ (^-^РЂ║-homo x oРѓѓ nРѓѓ)))
     РЪЕ
    РЪд x^ (oРѓЂ +РёЋ oРѓѓ) РѕЎ K (cРѓЂ *-nonzero cРѓѓ) РЪД x
    + РЪд cРѓЂ РѕЎрхќ x^ (oРѓЂ +РёЋ oРѓѓ +РёЋ РЪЁ nРѓѓ РЄЊРЪє) РѕЎ q РЪД x
    + (x ^ (oРѓЂ +РёЋ oРѓѓ) * x ^РЂ║ nРѓЂ) * (projРѓЂ cРѓѓ * РЪд p РЪД╦б x)
    + ((x ^ oРѓЂ * x ^РЂ║ nРѓЂ) * РЪд p РЪД╦б x) * ((x ^ oРѓѓ * x ^РЂ║ nРѓѓ) * РЪд q РЪД╦б x)
    РЅѕРЪе +-cong╩│ (+-cong (+-cong (*-cong╩│ (^-homo x oРѓЂ oРѓѓ))
                               (*-cong  (^-^РЂ║-homo x (oРѓЂ +РёЋ oРѓѓ) nРѓѓ)
                                        (РѕЎрхќ-spine-homo cРѓЂ q x)))
                       (*-cong╩│ (*-cong╩│ (^-homo x oРѓЂ oРѓѓ)))) РЪЕ
      (x ^ oРѓЂ * x ^ oРѓѓ) * (projРѓЂ cРѓЂ * projРѓЂ cРѓѓ)
    + (x ^ (oРѓЂ +РёЋ oРѓѓ) * x ^РЂ║ nРѓѓ) * (projРѓЂ cРѓЂ * РЪд q РЪД╦б x)
    + (x ^ oРѓЂ * x ^ oРѓѓ * x ^РЂ║ nРѓЂ) * (projРѓЂ cРѓѓ * РЪд p РЪД╦б x)
    + ((x ^ oРѓЂ * x ^РЂ║ nРѓЂ) * РЪд p РЪД╦б x) * ((x ^ oРѓѓ * x ^РЂ║ nРѓѓ) * РЪд q РЪД╦б x)
    РЅѕРЪе +-cong╩│ (+-cong╩│ (+-cong╦А (*-cong╩│ (*-cong╩│ (^-homo x oРѓЂ oРѓѓ))))) РЪЕ
      (x ^ oРѓЂ * x ^ oРѓѓ) * (projРѓЂ cРѓЂ * projРѓЂ cРѓѓ)
    + ((x ^ oРѓЂ * x ^ oРѓѓ) * x ^РЂ║ nРѓѓ) * (projРѓЂ cРѓЂ * РЪд q РЪД╦б x)
    + (x ^ oРѓЂ * x ^ oРѓѓ * x ^РЂ║ nРѓЂ) * (projРѓЂ cРѓѓ * РЪд p РЪД╦б x)
    + ((x ^ oРѓЂ * x ^РЂ║ nРѓЂ) * РЪд p РЪД╦б x) * ((x ^ oРѓѓ * x ^РЂ║ nРѓѓ) * РЪд q РЪД╦б x)
    РЅѕРЪе mult (x ^ oРѓЂ) (x ^ oРѓѓ) (x ^РЂ║ nРѓЂ) (x ^РЂ║ nРѓѓ) (projРѓЂ cРѓЂ) (projРѓЂ cРѓѓ) (РЪд p РЪД╦б x) (РЪд q РЪД╦б x) РЪЕ
    ((x ^ oРѓЂ) * (projРѓЂ cРѓЂ + (x ^РЂ║ nРѓЂ) * РЪд p РЪД╦б x)) *
    ((x ^ oРѓѓ) * (projРѓЂ cРѓѓ + (x ^РЂ║ nРѓѓ) * РЪд q РЪД╦б x)) Рѕј

-рхќРђ┐homo : Рѕђ p x Рєњ РЪд -рхќ p РЪД x РЅѕ - РЪд p РЪД x
-рхќРђ┐homo p x = beginРЪе setoid РЪЕ
  РЪд -1#-nonzero РѕЎрхќ p РЪД x РЅѕРЪе РѕЎрхќ-homo -1#-nonzero p x РЪЕ
  - 1# * РЪд p РЪД x         РЅѕРЪе sym (-Рђ┐distrib╦А-* 1# (РЪд p РЪД x)) РЪЕ
  - (1# * РЪд p РЪД x)       РЅѕРЪе -Рђ┐cong (*-identity╦А (РЪд p РЪД x)) РЪЕ
  - РЪд p РЪД x              Рѕј

+рхќ-cong : CongruentРѓѓ _+рхќ_
+рхќ-cong {p} {q} {r} {s} (РЅѕРюЊ Рѕђx[pРѓЊРЅѕqРѓЊ]) (РЅѕРюЊ Рѕђx[rРѓЊРЅѕsРѓЊ]) = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд p +рхќ r РЪД x РЅѕРЪе +рхќ-homo p r x РЪЕ
  РЪд p РЪД x + РЪд r РЪД x РЅѕРЪе +-cong (Рѕђx[pРѓЊРЅѕqРѓЊ] x) (Рѕђx[rРѓЊРЅѕsРѓЊ] x)  РЪЕ
  РЪд q РЪД x + РЪд s РЪД x РЅѕРЪе sym (+рхќ-homo q s x) РЪЕ
  РЪд q +рхќ s РЪД x Рѕј

+рхќ-isMagma : IsMagma _+рхќ_
+рхќ-isMagma = record
  { isEquivalence = РЅѕрхќ-isEquivalence
  ; РѕЎ-cong = +рхќ-cong
  }

+рхќ-assoc : Associative _+рхќ_
+рхќ-assoc p q r = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд p +рхќ q +рхќ r РЪД x РЅѕРЪе +рхќ-homo (p +рхќ q) r x РЪЕ
  РЪд p +рхќ q РЪД x + РЪд r РЪД x РЅѕРЪе +-cong╩│ (+рхќ-homo p q x) РЪЕ
  РЪд p РЪД x + РЪд q РЪД x + РЪд r РЪД x РЅѕРЪе +-assoc (РЪд p РЪД x) (РЪд q РЪД x) (РЪд r РЪД x) РЪЕ
  РЪд p РЪД x + (РЪд q РЪД x + РЪд r РЪД x) РЅѕРЪе +-cong╦А (sym (+рхќ-homo q r x)) РЪЕ
  РЪд p РЪД x + РЪд q +рхќ r РЪД x РЅѕРЪе sym (+рхќ-homo p (q +рхќ r) x) РЪЕ
  РЪд p +рхќ (q +рхќ r) РЪД x Рѕј

+рхќ-isSemigroup : IsSemigroup _+рхќ_
+рхќ-isSemigroup = record
  { isMagma = +рхќ-isMagma
  ; assoc = +рхќ-assoc
  }

+рхќ-comm : Commutative _+рхќ_
+рхќ-comm p q = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд p +рхќ q РЪД x РЅѕРЪе +рхќ-homo p q x РЪЕ
  РЪд p РЪД x + РЪд q РЪД x РЅѕРЪе +-comm (РЪд p РЪД x) (РЪд q РЪД x) РЪЕ
  РЪд q РЪД x + РЪд p РЪД x РЅѕРЪе sym (+рхќ-homo q p x) РЪЕ
  РЪд q +рхќ p РЪД x Рѕј

+рхќ-identity╦А : LeftIdentity 0рхќ _+рхќ_
+рхќ-identity╦А p = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд 0рхќ +рхќ p РЪД x РЅѕРЪе +рхќ-homo 0рхќ p x РЪЕ
  0# + РЪд p РЪД x РЅѕРЪе +-identity╦А (РЪд p РЪД x) РЪЕ
  РЪд p РЪД x Рѕј

open import Algebra.FunctionProperties.Consequences РЅѕрхќ-setoid using (comm+id╦АРЄњid╩│; comm+inv╦АРЄњinv╩│; comm+distr╦АРЄњdistr╩│)

+рхќ-identity╩│ : RightIdentity 0рхќ _+рхќ_
+рхќ-identity╩│ = comm+id╦АРЄњid╩│ +рхќ-comm +рхќ-identity╦А

+рхќ-identity : Identity 0рхќ _+рхќ_
+рхќ-identity = +рхќ-identity╦А , +рхќ-identity╩│

+рхќ-isMonoid : IsMonoid _+рхќ_ 0рхќ
+рхќ-isMonoid = record
  { isSemigroup = +рхќ-isSemigroup
  ; identity = +рхќ-identity
  }

-рхќРђ┐inverse╦А : LeftInverse 0рхќ -рхќ_ _+рхќ_
-рхќРђ┐inverse╦А p = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд -рхќ p +рхќ p РЪД x      РЅѕРЪе +рхќ-homo (-рхќ p) p x РЪЕ
  РЪд -рхќ p РЪД x + РЪд p РЪД x РЅѕРЪе +-cong╩│ (-рхќРђ┐homo p x) РЪЕ
  - РЪд p РЪД x + РЪд p РЪД x  РЅѕРЪе -Рђ┐inverse╦А (РЪд p РЪД x) РЪЕ
  РЪд 0рхќ РЪД x             Рѕј

-рхќРђ┐inverse╩│ : RightInverse 0рхќ -рхќ_ _+рхќ_
-рхќРђ┐inverse╩│ = comm+inv╦АРЄњinv╩│ +рхќ-comm -рхќРђ┐inverse╦А

-рхќРђ┐inverse : Inverse 0рхќ -рхќ_ _+рхќ_
-рхќРђ┐inverse = -рхќРђ┐inverse╦А , -рхќРђ┐inverse╩│

-рхќРђ┐cong : CongruentРѓЂ (-рхќ_)
-рхќРђ┐cong {p} {q} (РЅѕРюЊ Рѕђx[pРѓЊРЅѕqРѓЊ]) = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд -рхќ p РЪД x РЅѕРЪе -рхќРђ┐homo p x РЪЕ
  - РЪд p РЪД x  РЅѕРЪе -Рђ┐cong (Рѕђx[pРѓЊРЅѕqРѓЊ] x) РЪЕ
  - РЪд q РЪД x  РЅѕРЪе sym (-рхќРђ┐homo q x) РЪЕ
  РЪд -рхќ q РЪД x Рѕј

+рхќ-isGroup : IsGroup _+рхќ_ 0рхќ (-рхќ_)
+рхќ-isGroup = record
  { isMonoid = +рхќ-isMonoid
  ; inverse = -рхќРђ┐inverse
  ; РЂ╗┬╣-cong = -рхќРђ┐cong
  }

+рхќ-isAbelianGroup : IsAbelianGroup _+рхќ_ 0рхќ (-рхќ_)
+рхќ-isAbelianGroup = record
  { isGroup = +рхќ-isGroup
  ; comm = +рхќ-comm
  }

*рхќ-cong : CongruentРѓѓ _*рхќ_
*рхќ-cong {p} {q} {r} {s} (РЅѕРюЊ Рѕђx[pРѓЊРЅѕqРѓЊ]) (РЅѕРюЊ Рѕђx[rРѓЊРЅѕsРѓЊ]) = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд p *рхќ r РЪД x РЅѕРЪе *рхќ-homo p r x РЪЕ
  РЪд p РЪД x * РЪд r РЪД x РЅѕРЪе *-cong (Рѕђx[pРѓЊРЅѕqРѓЊ] x) (Рѕђx[rРѓЊРЅѕsРѓЊ] x)  РЪЕ
  РЪд q РЪД x * РЪд s РЪД x РЅѕРЪе sym (*рхќ-homo q s x) РЪЕ
  РЪд q *рхќ s РЪД x Рѕј

*рхќ-isMagma : IsMagma _*рхќ_
*рхќ-isMagma = record
  { isEquivalence = РЅѕрхќ-isEquivalence
  ; РѕЎ-cong = *рхќ-cong
  }

*рхќ-assoc : Associative _*рхќ_
*рхќ-assoc p q r = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд p *рхќ q *рхќ r РЪД x РЅѕРЪе *рхќ-homo (p *рхќ q) r x РЪЕ
  РЪд p *рхќ q РЪД x * РЪд r РЪД x РЅѕРЪе *-cong╩│ (*рхќ-homo p q x) РЪЕ
  РЪд p РЪД x * РЪд q РЪД x * РЪд r РЪД x РЅѕРЪе *-assoc (РЪд p РЪД x) (РЪд q РЪД x) (РЪд r РЪД x) РЪЕ
  РЪд p РЪД x * (РЪд q РЪД x * РЪд r РЪД x) РЅѕРЪе *-cong╦А (sym (*рхќ-homo q r x)) РЪЕ
  РЪд p РЪД x * РЪд q *рхќ r РЪД x РЅѕРЪе sym (*рхќ-homo p (q *рхќ r) x) РЪЕ
  РЪд p *рхќ (q *рхќ r) РЪД x Рѕј

*рхќ-isSemigroup : IsSemigroup _*рхќ_
*рхќ-isSemigroup = record
  { isMagma = *рхќ-isMagma
  ; assoc = *рхќ-assoc
  }

*рхќ-comm : Commutative _*рхќ_
*рхќ-comm p q = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд p *рхќ q РЪД x РЅѕРЪе *рхќ-homo p q x РЪЕ
  РЪд p РЪД x * РЪд q РЪД x РЅѕРЪе *-comm (РЪд p РЪД x) (РЪд q РЪД x) РЪЕ
  РЪд q РЪД x * РЪд p РЪД x РЅѕРЪе sym (*рхќ-homo q p x) РЪЕ
  РЪд q *рхќ p РЪД x Рѕј

*рхќ-identity╦А : LeftIdentity 1рхќ _*рхќ_
*рхќ-identity╦А p = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд 1рхќ *рхќ p РЪД x      РЅѕРЪе *рхќ-homo 1рхќ p x РЪЕ
  РЪд 1рхќ РЪД x * РЪд p РЪД x РЅѕРЪе *-cong╩│ (1рхќ-homo x) РЪЕ
  1# * РЪд p РЪД x       РЅѕРЪе *-identity╦А (РЪд p РЪД x) РЪЕ
  РЪд p РЪД x            Рѕј


*рхќ-identity╩│ : RightIdentity 1рхќ _*рхќ_
*рхќ-identity╩│ = comm+id╦АРЄњid╩│ *рхќ-comm *рхќ-identity╦А

*рхќ-identity : Identity 1рхќ _*рхќ_
*рхќ-identity = *рхќ-identity╦А , *рхќ-identity╩│

*рхќ-1рхќ-isMonoid : IsMonoid _*рхќ_ 1рхќ
*рхќ-1рхќ-isMonoid = record
  { isSemigroup = *рхќ-isSemigroup
  ; identity = *рхќ-identity
  }

*рхќ-distrib╦А-+рхќ : _*рхќ_ DistributesOver╦А _+рхќ_
*рхќ-distrib╦А-+рхќ p q r = РЅѕРюЊ ╬╗ x Рєњ beginРЪе setoid РЪЕ
  РЪд p *рхќ (q +рхќ r) РЪД x                   РЅѕРЪе *рхќ-homo p (q +рхќ r) x РЪЕ
  РЪд p РЪД x * РЪд q +рхќ r РЪД x                РЅѕРЪе *-cong╦А (+рхќ-homo q r x)  РЪЕ
  РЪд p РЪД x * (РЪд q РЪД x + РЪд r РЪД x)         РЅѕРЪе distrib╦А (РЪд p РЪД x) (РЪд q РЪД x) (РЪд r РЪД x) РЪЕ
  РЪд p РЪД x * РЪд q РЪД x + РЪд p РЪД x * РЪд r РЪД x РЅѕРЪе +-cong (sym (*рхќ-homo p q x)) (sym (*рхќ-homo p r x)) РЪЕ
  РЪд p *рхќ q РЪД x + РЪд p *рхќ r РЪД x           РЅѕРЪе sym (+рхќ-homo (p *рхќ q) (p *рхќ r) x) РЪЕ
  РЪд p *рхќ q +рхќ p *рхќ r РЪД x                Рѕј


*рхќ-distrib╩│-+рхќ : _*рхќ_ DistributesOver╩│ _+рхќ_
*рхќ-distrib╩│-+рхќ = comm+distr╦АРЄњdistr╩│ +рхќ-cong *рхќ-comm *рхќ-distrib╦А-+рхќ

*рхќ-distrib-+рхќ : _*рхќ_ DistributesOver _+рхќ_
*рхќ-distrib-+рхќ = *рхќ-distrib╦А-+рхќ , *рхќ-distrib╩│-+рхќ

+рхќ-*рхќ-isRing : IsRing _+рхќ_ _*рхќ_ -рхќ_ 0рхќ 1рхќ
+рхќ-*рхќ-isRing = record
  { +-isAbelianGroup = +рхќ-isAbelianGroup
  ; *-isMonoid = *рхќ-1рхќ-isMonoid
  ; distrib = *рхќ-distrib-+рхќ
  }

+рхќ-*рхќ-isCommutativeRing : IsCommutativeRing _+рхќ_ _*рхќ_ -рхќ_ 0рхќ 1рхќ
+рхќ-*рхќ-isCommutativeRing = record
  { isRing = +рхќ-*рхќ-isRing
  ; *-comm = *рхќ-comm
  }

0рхќРЅЅ1рхќ : 0рхќ РЅЅрхќ 1рхќ
0рхќРЅЅ1рхќ (РЅѕРюЊ Рѕђx[0РЅѕx^0*1])= 0#РЅЅ1# $ beginРЪе setoid РЪЕ
  0# РЅѕРЪе Рѕђx[0РЅѕx^0*1] 1# РЪЕ
  1# * 1# РЅѕРЪе *-identity╩│ 1# РЪЕ
  1# Рѕј

+рхќ-*рхќ-isNonZeroCommutativeRing : IsNonZeroCommutativeRing _+рхќ_ _*рхќ_ -рхќ_ 0рхќ 1рхќ
+рхќ-*рхќ-isNonZeroCommutativeRing = record
  { isCommutativeRing = +рхќ-*рхќ-isCommutativeRing
  ; 0#РЅЅ1# = 0рхќРЅЅ1рхќ
  }


-- xРЅЅ0РЄњx^nРЅЅ0 : Рѕђ x (xРЅЅ0 : x РЅЅ 0#) n Рєњ x ^ n РЅЅ 0#
-- xРЅЅ0РЄњx^nРЅЅ0 = {!!}

-- *рхќ-cancel╦А : Рѕђ p {q r} Рєњ p РЅЅрхќ 0рхќ Рєњ p *рхќ q РЅѕрхќ p *рхќ r Рєњ q РЅѕрхќ r
-- *рхќ-cancel╦А 0рхќ {q} {r} pРЅЅ0 (РЅѕРюЊ Рѕђx[p*qРЅѕp*r]) = contradiction РЅѕрхќ-refl pРЅЅ0
-- *рхќ-cancel╦А (x^ n РѕЎ p) {q} {r} pРЅЅ0 (РЅѕРюЊ Рѕђx[p*qРЅѕp*r]) = РЅѕРюЊ Рѕђx[qРѓЊРЅѕrРѓЊ]
--   where
--   Рѕђx[qРѓЊРЅѕrРѓЊ] : Рѕђ x Рєњ РЪд q РЪД x РЅѕ РЪд r РЪД x
--   Рѕђx[qРѓЊРЅѕrРѓЊ] x with x РЅѕ? 0#
--   ... | yes xРЅѕ0 = {!!}
--   ... | no  xРЅЅ0 = *-cancel╦А (x ^ n) (xРЅЅ0РЄњx^nРЅЅ0 x xРЅЅ0 n) {!!}

-- +рхќ-*рхќ-isIntegralDomain : IsIntegralDomain _+рхќ_ _*рхќ_ -рхќ_ 0рхќ 1рхќ
-- +рхќ-*рхќ-isIntegralDomain = record
--   { isNonZeroCommutativeRing = +рхќ-*рхќ-isNonZeroCommutativeRing
--   ; *-cancel╦А = {!!}
--   }

-- open import AKS.Unsafe using (TODO)

-- lc : Рѕђ p {pРЅЅ0 : p РЅЅрхќ 0рхќ} Рєњ C/0
-- lc 0рхќ {pРЅЅ0} = contradiction РЅѕрхќ-refl pРЅЅ0
-- lc (x^ n РѕЎ p) {pРЅЅ0} = lc-spine p
--   where
--   lc-spine : Spine Рєњ C/0
--   lc-spine (K c) = c
--   lc-spine (c +x^ n РѕЎ p) = lc-spine p

-- infix 4 _РЅѕрхќ?_
-- _РЅѕрхќ?_ : Decidable _РЅѕрхќ_
-- _divрхќ_ : Рѕђ (n m : Polynomial) {mРЅЅ0 : m РЅЅрхќ 0рхќ} Рєњ Polynomial

-- (n divрхќ m) {mРЅЅ0} with n РЅѕрхќ? 0рхќ
-- ... | yes nРЅѕ0 = 0рхќ
-- ... | no  nРЅЅ0 = loop 0рхќ n {nРЅЅ0} <-well-founded
--   where
--   leading : Рѕђ r {rРЅЅ0 : r РЅЅрхќ 0рхќ} Рєњ Polynomial
--   leading r {rРЅЅ0} = (lc r {rРЅЅ0} /-nonzero lc m {mРЅЅ0}) РѕЎ­ЮЉІ^ (deg r {rРЅЅ0} РѕИ deg m {mРЅЅ0})
--   loop : Рѕђ (q r : Polynomial) {rРЅЅ0} Рєњ Acc _<_ (deg r {rРЅЅ0}) Рєњ Polynomial
--   loop q r {rРЅЅ0} (acc downward) with <-РЅц-connex (deg r {rРЅЅ0}) (deg m {mРЅЅ0})
--   ... | injРѓЂ r<m = q
--   ... | injРѓѓ rРЅЦm with r -рхќ leading r {rРЅЅ0} *рхќ m РЅѕрхќ? 0рхќ
--   ...   | yes r'РЅѕ0 = q
--   ...   | no  r'РЅЅ0 = loop (q +рхќ leading r {rРЅЅ0}) (r -рхќ leading r {rРЅЅ0} *рхќ m) {r'РЅЅ0} (downward _ TODO)

-- РЅѕрхќРЄњРЅѕРЂ▒ : Рѕђ {p q} Рєњ p РЅѕрхќ q Рєњ p РЅѕРЂ▒ q
-- РЅѕрхќРЄњРЅѕРЂ▒ {0рхќ} {0рхќ} pРЅѕрхќq = 0рхќРЅѕ
-- РЅѕрхќРЄњРЅѕРЂ▒ {0рхќ} {x^ oРѓѓ РѕЎ q} (РЅѕРюЊ Рѕђx[pРѓЊРЅѕqРѓЊ]) = TODO
-- РЅѕрхќРЄњРЅѕРЂ▒ {x^ oРѓЂ РѕЎ p} {0рхќ} pРЅѕрхќq = TODO
-- РЅѕрхќРЄњРЅѕРЂ▒ {x^ oРѓЂ РѕЎ p} {x^ oРѓѓ РѕЎ q} (РЅѕРюЊ Рѕђx[pРѓЊРЅѕqРѓЊ]) = 0рхќРЅЅ {!!} {!!}

-- p РЅѕрхќ? q = map (FE.equivalence РЅѕРЂ▒РЄњРЅѕрхќ РЅѕрхќРЄњРЅѕРЂ▒) (p РЅѕРЂ▒? q)
