\documentclass[./Thesis.tex]{subfiles}
\begin{document}

\chapter{Exponentiation}
\label{chap:exponentiation}

\epigraph{
  Accurate reckoning. \\
  The entrance into the knowledge of all existing
  things and all obscure secrets.
}{
  Translated from the Rhind Egyptian Mathematical Papyrus.
  \cite{rhind-papyrus}
}

In this chapter we will investigate formalizing exponentiation and the
algorithms to compute it. As \Agda{} is a proof theoretic language these things
are one and the same \cite{hott-book}.

\section{Inductive Definitions}
\label{sec:inductive-definitions}

\begin{code}[hide]
  open import Algebra using (CommutativeMonoid)
  module Exponentiation where
  open import Relation.Nullary.Decidable using (False)
  open import Relation.Nullary.Negation using (contradiction)
  open import AKS.Nat using (ℕ; zero; suc; _+_; _*_; _<_; lte; _≟_)
  open import AKS.Nat using (n%m<m)
  open import Data.Nat.Properties using (*-identityˡ; *-assoc; *-comm)
  open import Relation.Binary.PropositionalEquality using (_≡_; _≢_; module ≡-Reasoning) renaming (refl to ≡-refl; sym to ≡-sym; cong to ≡-cong)
\end{code}

Before formalizing a piece of mathematics its often useful to step
back and think about the desired semantics of your formalism. In this case we
ask the question what are the semantics of exponentiation. Middle and Elementary
students often learn that exponentiation is ``repeated'' multiplication. This
thought is made more precise below:
\begin{align}
  \label{eqn:exp-first-idea}
  x^n = \underbrace{x \times x \times \dots \times x}_{n - 1 \text{ multiplications}}
\end{align}
The equation above lacks parentheses, arbitrarily assigning right precedence
the equation becomes:
\begin{align}
  \label{eqn:exp-second-idea}
  x^n = x \times (x \times (x \times \dots (x \times x) \dots ))
\end{align}
This leads to an obvious inductive definition to compute $x^n$ just multply $x$
by $x^{n -1}$ with a base case of $x^0 = 1$. This definition cleanly bakes a law
of exponents $x^{1+n} = x^1 \times x^n = x \times x^n$ into the definition. This
idea is formalized below. \\

\begin{code}[hide]
  module Basic (C : Set) where
    open ≡-Reasoning 
    infixr 8 _^_
\end{code}
\begin{code}
    _^_ : ℕ → ℕ → ℕ
    x ^ zero = 1
    x ^ suc n = x * x ^ n
\end{code} \\

We demonstrate the correctness of this definition by proving one of the
fundemental laws of exponentiation,
the exponentiational homomorphism $x^{n + m} = x^n x^m$.
\begin{code}
    ^-homo : ∀ x n m → x ^ (n + m) ≡ x ^ n * x ^ m
\end{code}
The type above just makes the quantifiers explict. The base case below
is more intresting. We first simplify and unwrap the definitons then
tac a $1$ onto the exponential. This is allowable as $1$ is the
multpliciative unit. Finally we rewrap definitions, notice that
$x \string^{} \, 0$ is defined to be $1$ so the simplifier can work in reverse.
\begin{code}
    ^-homo x zero m = begin
      x ^ (0 + m)   ≡⟨⟩
      x ^ m         ≡⟨ ≡-sym (*-identityˡ (x ^ m)) ⟩
      1 * x ^ m     ≡⟨⟩
      x ^ 0 * x ^ m ∎
\end{code}
The inductive step starts similarlly to the base case in so far as we
unfold the defintions. Then we invoke the inductive hypothesis which in \Agda{}
is just a recursive call to our proof. Notice that the induction is well founded
as the natural $n$ is decreasing by one each inductive step.
Lastly we associate the multiplications correctly.
\begin{code}
    ^-homo x (suc n) m = begin
      x ^ (suc n + m)     ≡⟨⟩
      x ^ suc (n + m)     ≡⟨⟩
      x * x ^ (n + m)     ≡⟨ ≡-cong (λ e → x * e) (^-homo x n m) ⟩
      x * (x ^ n * x ^ m) ≡⟨ ≡-sym (*-assoc x (x ^ n) (x ^ m)) ⟩
      (x * x ^ n) * x ^ m ≡⟨⟩
      (x ^ suc n) * x ^ m ∎
\end{code}
\section{Don't Repeat Yourself}
\label{sec:dont-repeat-yourself}
In the previous section we assumed that the base of our exponential was a
natural number. This definition restricts even basic mathematics as expressions
like $(-1)^3$ and $\pi^2$ are not well typed. A mathematician might suggest
using a base of complex numbers as the naturals, integers, and reals are all
subclasses of $\mathbb{C}$. This solution has two problems, firstly subtyping
(the type theoretic analog of subclassing) is an open problem in a language like
\Agda{} \cite{algebraic-subtyping}. Secondly there are types which are not
subclasses of $\mathbb{C}$ that we would like to exponentiate. A notable example
from linear algebra, the $n$th entry of the Fibonacci sequence can be found by
raising a certain matrix to the $n$th power \cite{linear-algebra}.
\begin{align}
  \label{eqn:fib-example}
  \rowarrowsep=-2pt
  \begin{gmatrix}[p]
    F_{n - 1} & F_{n} \\
    F_{n} & F_{n + 1}
  \end{gmatrix}
  =
  {
  \begin{gmatrix}[p]
    0 & 1 \\
    1 & 1
  \end{gmatrix}
  }^n
\end{align}
In the coming chapters we will need to exponentiate non-number types so we need
to step back and generalize our base. In the previous section the proofs
required almost no properties of multiplication. The proofs required an
associative binary operation equipped with an identity element. Those
constraints basically define a monoid. A quick sanity check shows that all the
types listed above form a monoid under their respective multiplication
operators. \\

We can generalize our definition by creating a module that takes any monoid.
\begin{code}[hide]
  open import Algebra using (Monoid; CommutativeMonoid)
  open import Relation.Binary using (Setoid)
\end{code}
\begin{code}
  module Exponentiation₁ {c ℓ} (M : Monoid c ℓ) where
    open Monoid M public
      using (ε; _∙_; ∙-cong; ∙-congˡ; ∙-congʳ; identityˡ; identityʳ; assoc)
      using (_≈_; setoid)
      renaming (Carrier to C)
    open Setoid setoid public using (refl; sym)
    open import Relation.Binary.Reasoning.Setoid setoid public
\end{code}
From this point on bases are values of type C, the
binary operation is $∙$, and the identity element is
$\varepsilon$. This leads to a more general inductive definition:
\begin{code}
    _^_ : C → ℕ → C
    x ^ zero = ε
    x ^ suc n = x ∙ x ^ n
\end{code}
We can update the homomorphism proof as well keeping in mind now we are working
over an arbitrary setoid instead of the built in equality type. This is
reflected in the following new type of $\AgdaFunction{\^{}-homo}$.
% TODO: Define setoid
\begin{code}[hide]
    infixr 8 _^_
\end{code}
\begin{code}
    ^-homo : ∀ x n m → x ^ (n + m) ≈ x ^ n ∙ x ^ m
\end{code}
The base case closely resembles the original proof with one change. Adding on
the identity element is only true up to setoid equality. This is
reflected in the combinator used to string the proof steps together.
\begin{code}
    ^-homo x zero m = begin
      x ^ (0 + m)   ≡⟨⟩
      x ^ m         ≈⟨ sym (identityˡ (x ^ m)) ⟩
      ε ∙ x ^ m     ≡⟨⟩
      x ^ 0 ∙ x ^ m ∎
\end{code}
Not all functions are congruent over an arbitrary setoid, so you need to prove
congruence for specific functions. Luckily by definition all monodial binary
operations are congruent. This fact is witnessed by the
proof generating function $\AgdaFunction{∙-congˡ}$.
\begin{code}
    ^-homo x (suc n) m = begin
      x ^ (suc n + m)     ≡⟨⟩
      x ^ suc (n + m)     ≡⟨⟩
      x ∙ x ^ (n + m)     ≈⟨ ∙-congˡ (^-homo x n m) ⟩
      x ∙ (x ^ n ∙ x ^ m) ≈⟨ sym (assoc x (x ^ n) (x ^ m)) ⟩
      (x ∙ x ^ n) ∙ x ^ m ≡⟨⟩
      (x ^ suc n) ∙ x ^ m ∎
\end{code}
Another core law of exponentiation is the fact that exponentiation distributes
over multiplication or in our case the binary operator
$(x \cdot y)^n = x^n \cdot y^n$. Unfortunately this law is false over
non-commutative monoids. For instance the free monoid, commonly called the list
data structure \cite{awodey}, is a counterexample to this law:
\begin{align}
  \label{eqn:dist-counterexample}
       ([1] \, \diamond \, [2])^2
     = [1, 2]^2
     = [1, 2, 1, 2]
  \neq [1, 1, 2, 2]
     = [1, 1] \, \diamond \, [2, 2]
     = [1]^2 \, \diamond \, [2]^2
\end{align}
This means we need to strength our assumptions and work with commutative
monoids. As every commutative monoid is a monoid all our previous work holds
true, and this can be witnessed by passing the correct modules around.
Note that we now have access to the $\AgdaFunction{comm}$ axiom, the proof that the
binary operator is commutative. 
\begin{code}
  module Exponentiation₂ {c ℓ} (M : CommutativeMonoid c ℓ) where
    open CommutativeMonoid M using (monoid; comm) public
    open Exponentiation₁ monoid public
\end{code}
The type and base case for this proof are quite similar to the proof above. Note
that for any monoid $\varepsilon = \varepsilon^2$.
\begin{code}
    ∙-^-dist : ∀ x y n → (x ∙ y) ^ n ≈ x ^ n ∙ y ^ n
    ∙-^-dist x y zero = begin
      (x ∙ y) ^ 0   ≡⟨⟩
      ε             ≈⟨ sym (identityˡ ε) ⟩
      ε ∙ ε         ≡⟨⟩
      x ^ 0 ∙ y ^ 0 ∎
\end{code}
The inductive step contains the meat of this proof. We first apply the inductive
hypothesis then we reassociate the terms until $y$ and $x \,\,\, \hat{} \,\,\, n$ are siblings.
Only then can we apply the commutativity axiom. Note the repeated uses of
$\AgdaFunction{∙-cong}$ to ignore irrelevant parts of expression tree. The proof concludes by
reassociating the terms in reverse. 
\begin{code}
    ∙-^-dist x y (suc n) = begin
      (x ∙ y) ^ suc n           ≡⟨⟩
      (x ∙ y) ∙ (x ∙ y) ^ n     ≈⟨ ∙-congˡ (∙-^-dist x y n) ⟩
      (x ∙ y) ∙ (x ^ n ∙ y ^ n) ≈⟨ assoc x y (x ^ n ∙ y ^ n) ⟩
      x ∙ (y ∙ (x ^ n ∙ y ^ n)) ≈⟨ ∙-congˡ (sym (assoc y (x ^ n) (y ^ n))) ⟩
      x ∙ ((y ∙ x ^ n) ∙ y ^ n) ≈⟨ ∙-congˡ (∙-congʳ (comm y (x ^ n))) ⟩
      x ∙ ((x ^ n ∙ y) ∙ y ^ n) ≈⟨ ∙-congˡ (assoc (x ^ n) y (y ^ n)) ⟩
      x ∙ (x ^ n ∙ (y ∙ y ^ n)) ≈⟨ sym (assoc x (x ^ n) (y ∙ y ^ n)) ⟩
      (x ∙ x ^ n) ∙ (y ∙ y ^ n) ≡⟨⟩
      x ^ suc n ∙ y ^ suc n     ∎
\end{code}
We end this section by analyzing the run time of this algorithm. The run time
can be represented by this simple recurrence. Assume that the binary operation
runs in $\BigO{1}$ time\footnote
{In general this assumption is false but we
  will use this algorithm to exponentiate modular rings.
}.
\begin{align}
  \label{eqn:slow-recurrence}
  \begin{split}
    T(0) &= \BigO{1} \\
    T(n) &= T(n - 1) + \BigO{1}
  \end{split}
\end{align}
This recurrence clearly solves to $T(n) = \BigO{n}$. This linear result obscures
the exponential run time of this algorithm. The key to realize this is to ask
``What does n represent in that recurrance?'' It represents the full integer not
the length of the integer's binary representation. This insight shows that an inductively
defined exponential algorithm will run exponentially slower then run time of the
binary operation.
\section{Exponential Speedup}
\label{sec:exponential-speedup}
The solution to this exponential problem can be found by raising a base to
powers of 2. In the following equation assume $n$ is a power of two $n = 2^\ell$:
\begin{align}
  \label{eqn:squaring-even}
  x^n = x^{2^{\, \ell}}
  {}  = x^{2 \cdot 2^{\ell - 1}}
  {}  = \Parens*{x^2}^{2^{\ell - 1}}
  {}  = \Parens*{x^2}^{n / 2}
\end{align}
This equation provides a simple algorithm to exponentiate a base to a power of
two. Just square the base and recurse on half the exponent. The run time of this
algorithm is modeled by the recurrence given below.
\begin{align}
  \label{eqn:power-of-two-recurrance}
  \begin{split}
    T(0) &= \BigO{1} \\
    T(n) &= T(n / 2) + \BigO{1}
  \end{split}
\end{align}
The well known recurrence yields a complexity of $T(n) = \BigO{\Lg{n}}$. This is an
exponential improvement. Unfortunately this algorithm only works correctly for
powers of two. It's important to realize why this algorithm only works correctly
for powers of two. Note that equation \ref{eqn:squaring-even} requires n to be
even. The defining feature of powers of two is that they are all even,
disregarding $1$. So if we can find a parallel equation to equation
\ref{eqn:squaring-even} for odd numbers we will be able to exponentiate any power
quickly.
\begin{align}
  \label{eqn:squaring-odd}
  x^n = x^{n - 1 + 1} = x x^{n - 1} = x x^{2\Parens*{n - 1} / 2} = x \Parens*{x^2}^{\Parens*{n - 1} / 2}
\end{align}
We can combine these two ideas \ref{eqn:squaring-even}, \ref{eqn:squaring-odd}
into a single cased based definition.
\begin{align}
  \label{eqn:squaring-def-division}
  \begin{split}
    x^0 &= 1 \\
    x^n &=
    \begin{cases}
      \Parens*{x^2}^{n / 2} & n \text{ is even} \\
      x \Parens*{x^2}^{\Parens*{n - 1} / 2} & n \text{ is odd}
    \end{cases}
  \end{split}
\end{align}
This leads straight into a similar cased based recurrence.
\begin{align}
  \label{eqn:squaring-recurrance}
  \begin{split}
    T(0) &= \BigO{1} \\
    T(n) &=
    \begin{cases}
      T(n / 2) + \BigO{1} & n \text{ is even} \\
      T(n / 2) + 2 \BigO{1} & n \text{ is odd} \\
  \end{cases}
  \end{split}
\end{align}
The cases in this recurrence collapse together as the constant $2$ folds into
the big o term $\BigO{1}$. This fact makes this recurrence equal to
\ref{eqn:power-of-two-recurrance} and so they have equal run time.
\section{A Binary Digression}
\label{sec:a-binary-digression}
We could attempt to translate definition \ref{eqn:squaring-def-division} directly to
\Agda{}. While this is possible the definition obstructs proof writing as
checking for parity and integer division are non trivial constructions in
\Agda{}. Instead we can apply a couple of well known tricks in the world
of bitwise programming. Firstly division by two is the same as bit shift right ($\gg$)
by 1. In the following code $n_i$ represents the $i$th bit of $n$ and
$n_{\ell - 1} n_{\ell - 2} \dots n_{1} n_{0}$ represents the bitstring of $n$.
\begin{align}
  \Floor{n / 2}
  {} &= \Floor[\big]{\Parens[\big]{n_{\ell - 1} n_{\ell - 2} \dots n_{1} n_{0}} / 2} \\
  {} &= \Floor[\big]{\Parens[\big]{2^{\ell - 1}n_{\ell - 1} + 2^{\ell - 2} n_{\ell - 2} + \dots + 2^1 n_1 + 2^0 n_0} / 2} \\
  {} &= \Floor[\big]{\Parens[\big]{2^{\ell - 1}n_{\ell - 1}} / 2}
      + \Floor[\big]{\Parens[\big]{2^{\ell - 2} n_{\ell - 2}} / 2}
      + \dots
      + \Floor[\big]{\Parens[\big]{2^1 n_1} / 2}
      + \Floor[\big]{\Parens[\big]{2^0 n_0} / 2} \\
  {} &= 2^{\ell - 2}n_{\ell - 1}
      + 2^{\ell - 3} n_{\ell - 2}
      + \dots
      + 2^0 n_1 \\
  {} &= n_{\ell - 2} n_{\ell - 3} \dots n_{1} = n \gg 1
\end{align}
The term $\Floor[\big]{\Parens[\big]{2^0 n_0} / 2}$ disappears because the numerator is less
than $2$, a quirk of integer division. By a similar analysis shown above the
parity of an integer can be determined by the first bit of the integer's binary
representation. If the first bit is set then the integer is odd. These two
tricks allow us to rewrite our definition in terms \Agda{} will like.
\begin{align}
  \label{eqn:squaring-def-shift}
  \begin{split}
    x^0 &= 1 \\
    x^n &=
    \begin{cases}
      \Parens*{x^2}^{n \gg 1} & n_0 = 0𝕓 \\
      x \Parens*{x^2}^{n \gg 1} & n_0 = 1𝕓
    \end{cases}
  \end{split}
\end{align}
\Agda{} does not come with bitwise operator primitives so we will need to build
our own binary number type. Note that at every level of recursion we only need
to inspect the last element of the bitstring. Then we drop that element.
Thus if we store the binary number as a snoc list we can perform the
required operations in $\BigO{1}$. A snoc list is a purely functional linked
list that supports push/pop operations from the back of the list in
$\BigO{1}$ time \cite{okasaki}. The figure below is a snoc list encoding of the
number $10$.
\begin{align}
  \label{eqn:snoc-binary-10}
  [] :: 1𝕓 :: 0𝕓 :: 1𝕓 :: 0𝕓
\end{align}
This encoding has one major flaw. Numbers do not have a unique encoding as a user
can add arbitrarily many leading zeros. Take for instance the two encodings of
$3$ given below.
\begin{align}
  [] :: 0𝕓 :: 1𝕓 :: 1𝕓 \stackrel{?}{=} [] :: 1𝕓 :: 1𝕓
\end{align}
We can fix this issue by fusing the empty list and the most significant digit
together \cite{donnacha}. This idea generates a simple \Agda{} data type, note
that $\AgdaInductiveConstructor{𝕓1ᵇ}$ is the fused based case.
\begin{code}
  infixl 5 _0ᵇ _1ᵇ
  data 𝔹⁺ : Set where
    𝕓1ᵇ : 𝔹⁺
    _0ᵇ _1ᵇ : 𝔹⁺ → 𝔹⁺
\end{code}
This encoding unfortunately forbids any leading zeros, and the number $0$
requires exactly one leading zero. We can special case $0$ with its own
constructor.
\begin{code}
  infixr 4 +_
  data 𝔹 : Set where
    𝕓0ᵇ : 𝔹
    +_ : 𝔹⁺ → 𝔹

  three : 𝔹
  three = + 𝕓1ᵇ 1ᵇ
\end{code}
The following code defines a function to reify a binary encoded natural number
into \Agda{} primitive naturals. We define a helper function to multiply a number
by two. We could have used the term $2 * n$ but the simplifier transforms that
expression into $n + (n + 0)$. This expression unnecessarily complicates proofs
in the following sections.
\begin{code}
  2* : ℕ → ℕ
  2* n = n + n

  ⟦_⇓⟧⁺ : 𝔹⁺ → ℕ
  ⟦ 𝕓1ᵇ  ⇓⟧⁺ = 1
  ⟦ x 0ᵇ ⇓⟧⁺ = 2* ⟦ x ⇓⟧⁺
  ⟦ x 1ᵇ ⇓⟧⁺ = 1 + 2* ⟦ x ⇓⟧⁺

  ⟦_⇓⟧ : 𝔹 → ℕ
  ⟦ 𝕓0ᵇ ⇓⟧ = 0
  ⟦ + x ⇓⟧ = ⟦ x ⇓⟧⁺

  evaluation-test : ⟦ + 𝕓1ᵇ 0ᵇ 1ᵇ 0ᵇ ⇓⟧ ≡ 10
  evaluation-test = ≡-refl
\end{code}
In order to develop a way to upcast naturals to our binary represenation we
need to introduce an formal specification of integer division. In order
to make use of integer division we usually require both the quotient
and remainder so we create a type that bundles these results together. This type
also contains a proof of correctness of the division algorithm along with a
proof that the remainder is less than the divisor.
\begin{code}
  record Euclidean (n : ℕ) (m : ℕ) : Set where
    constructor Euclidean✓
    field
      q : ℕ
      r : ℕ
      division : n ≡ r + m * q
      r<m : r < m
\end{code}
The code above is just a type, we also require a function that produces the type
for a given dividend and divisor. We must ensure that the divisor is not equal
to zero, to do this we introduce two new concepts. A boolean function
$\AgdaFunction{\_≟\_}$ which returns true if the two inputs are equal. As well
as a type which is only inhabited if a boolean function returns
$\AgdaDatatype{false}$. These two concepts combine to require that any user of
the $\AgdaFunction{\_div\_}$ proves that the divisor is not equal to zero. The
actual implementation of this function is omitted as it makes use of complicated
primitive functions provided by \Agda{}.
\begin{code}[hide]
  open import Data.Nat.DivMod using (_/_; _%_; m≡m%n+[m/n]*n)
  open import Relation.Binary.PropositionalEquality.TrustMe using (trustMe)
\end{code}
\begin{code}
  _div_ : ∀ (n m : ℕ) {≢0 : False (m ≟ 0)} → Euclidean n m
\end{code}
\begin{code}[hide]
  n div (suc m) = Euclidean✓ (n / suc m) (n % suc m) div-proof (n%m<m n (suc m))
    where
    div-proof : n ≡ n % suc m + suc m * (n / suc m)
    div-proof rewrite *-comm (suc m) (n / suc m) = m≡m%n+[m/n]*n n m
\end{code}
Now we can write a function that reifies non zero naturals into our binary
representation. We begin by dividing the input by two. Note that we can
determine the parity of the input by observing the remainder after dividing by
two. In the case where the remainder is $0$ we know the input is even so we
recurse adding a $\AgdaInductiveConstructor{0ᵇ}$ to the result. \Agda{} infers
that the quotient must be greater then zero as the input is greater then zero.
When the remainder is equal to $1$ the quotient could be $0$ so we perform a
case analysis. If the quotient is zero we introduce our base case of
$\AgdaInductiveConstructor{𝕓1ᵇ}$ otherwise we recurse and add
$\AgdaInductiveConstructor{1ᵇ}$ to the result as the input was odd.
\begin{code}[hide]
  {-# TERMINATING #-}
\end{code}
\begin{code}
  ⟦_⇑⟧⁺ : ∀ (n : ℕ) {≢0 : False (n ≟ 0)} → 𝔹⁺
  ⟦ suc n ⇑⟧⁺ with suc n div 2
  ... | Euclidean✓ (suc q) 0 _ _ = ⟦ suc q ⇑⟧⁺ 0ᵇ
  ... | Euclidean✓ zero    1 _ _ = 𝕓1ᵇ
  ... | Euclidean✓ (suc q) 1 _ _ = ⟦ suc q ⇑⟧⁺ 1ᵇ

  ⟦_⇑⟧ : ℕ → 𝔹
  ⟦ zero ⇑⟧ = 𝕓0ᵇ
  ⟦ suc n ⇑⟧ = + ⟦ suc n ⇑⟧⁺
\end{code}
Lastly we need a proof that $\AgdaFunction{⟦\_⇓⟧}$ and $\AgdaFunction{⟦\_⇑⟧}$
form an isomorphism. After applying the inductive hypothesis the proof
can be offloaded to an automated ring solver. For this reason we exclude
the body of the proof.
\begin{code}
  ℕ→𝔹→ℕ : ∀ n → ⟦ ⟦ n ⇑⟧ ⇓⟧ ≡ n
\end{code}
\begin{code}[hide]
  ℕ→𝔹→ℕ n = trustMe
\end{code}
\section{Mutally Recursive Proofs}
\label{sec:mutally-recursive-proofs}
In this section we redefine our exponential inline with
\ref{eqn:squaring-def-shift}. Then we prove that the fast exponential is
equivalent to the inductively defined exponential up to setoid equality. In
order to do this we need to rename the old exponential and all its associated
proofs. To accomplish this we tac an (i)nductive onto all the old functions and proofs.
\begin{code}
  module Exponentiation₃ {c ℓ} (M : CommutativeMonoid c ℓ) where
    open Exponentiation₂ M
      renaming (_^_ to _^ⁱ_; ^-homo to ^ⁱ-homo; ∙-^-dist to ∙-^ⁱ-dist)
\end{code}
With that renaming out of the way the definitions follow simply from
\ref{eqn:squaring-def-shift}.
\begin{code}
    _^ᵇ⁺_ : C → 𝔹⁺ → C
    x ^ᵇ⁺ 𝕓1ᵇ = x
    x ^ᵇ⁺ (b 0ᵇ) = (x ∙ x) ^ᵇ⁺ b
    x ^ᵇ⁺ (b 1ᵇ) = x ∙ (x ∙ x) ^ᵇ⁺ b

    _^ᵇ_ : C → 𝔹 → C
    x ^ᵇ 𝕓0ᵇ = ε
    x ^ᵇ (+ b) = x ^ᵇ⁺ b

    _^_ : C → ℕ → C
    x ^ n = x ^ᵇ ⟦ n ⇑⟧
\end{code}
As $\AgdaFunction{\_\^{}ᵇ\_}$ is defined by recursion on a value of type
$\AgdaDatatype{𝔹}$ any proof about $\AgdaFunction{\_\^{}ᵇ\_}$ will have to case
on an input of type $\AgdaDatatype{𝔹}$ in order for the proof term to simplify.
So we create a lemma $\AgdaFunction{loop}$ that is true for any value of type
$\AgdaDatatype{𝔹}$ then we instantiate that lemma at $⟦ \, n ⇑⟧ :
\AgdaDatatype{𝔹}$. This will leave a term of the form $⟦ \, ⟦ \, n ⇑⟧ ⇓⟧$ which is
isomorphic to $n$.
\begin{code}
    x^n≈x^ⁱn : ∀ x n → x ^ n ≈ x ^ⁱ n
    x^n≈x^ⁱn x n = begin
      x ^ n ≈⟨ loop x ⟦ n ⇑⟧ ⟩
      x ^ⁱ ⟦ ⟦ n ⇑⟧ ⇓⟧ ≡⟨ ≡-cong (λ t → x ^ⁱ t) (ℕ→𝔹→ℕ n) ⟩
      x ^ⁱ n ∎
\end{code}
\begin{code}[hide]
      where
\end{code}
This lemma only proves the desired result for values of type $\AgdaDatatype{𝔹}$
we require another similar lemma (produced below) for values of type $\AgdaDatatype{𝔹⁺}$.
\begin{code}
        loop : ∀ x b → x ^ᵇ b ≈ x ^ⁱ ⟦ b ⇓⟧
        loop x 𝕓0ᵇ = refl
        loop x (+ b) = begin
          x ^ᵇ (+ b)    ≡⟨⟩
          x ^ᵇ⁺ b       ≈⟨ loop⁺ x b ⟩
          x ^ⁱ ⟦ b ⇓⟧⁺  ≡⟨⟩
          x ^ⁱ ⟦ + b ⇓⟧ ∎
\end{code}
\begin{code}[hide]
          where
\end{code}
Cases two and three simplify to extremely similar expressions so we factored the
common pattern out into the function $\AgdaFunction{even}$. This has a problem
though, $\AgdaFunction{even}$ calls $\AgdaFunction{loop⁺}$ and
$\AgdaFunction{loop⁺}$ calls $\AgdaFunction{even}$. This pattern is called
mutual recursion, and \Agda{} supports it nicely. To signal to \Agda{} that two
functions are mutually recursive you just declare both their type signatures
before providing a body to either function.
\begin{code}
          even : ∀ x b → (x ∙ x) ^ᵇ⁺ b ≈ x ^ⁱ 2* ⟦ b ⇓⟧⁺
          loop⁺ : ∀ x b → x ^ᵇ⁺ b ≈ x ^ⁱ ⟦ b ⇓⟧⁺
\end{code}
The idea behind $\AgdaFunction{even}$ is quite simple, we just apply the
inductive hypothesis and then we can work with proofs about the inductively
defined exponential.
\begin{code}
          even x b = begin
            (x ∙ x) ^ᵇ⁺ b               ≈⟨ loop⁺ (x ∙ x) b ⟩
            (x ∙ x) ^ⁱ ⟦ b ⇓⟧⁺          ≈⟨ ∙-^ⁱ-dist x x ⟦ b ⇓⟧⁺  ⟩
            x ^ⁱ ⟦ b ⇓⟧⁺ ∙ x ^ⁱ ⟦ b ⇓⟧⁺ ≈⟨ sym (^ⁱ-homo x ⟦ b ⇓⟧⁺ ⟦ b ⇓⟧⁺) ⟩
            x ^ⁱ (⟦ b ⇓⟧⁺ + ⟦ b ⇓⟧⁺)    ≡⟨⟩
            x ^ⁱ 2* ⟦ b ⇓⟧⁺            ∎
\end{code}
The simplifier can deduce most $\AgdaFunction{loop⁺}$ and then we apply
$\AgdaFunction{even}$. The only difference in the even and odd cases is a
single $x$ on the outside of the proof.
\begin{code}
          loop⁺ x 𝕓1ᵇ = begin
            x ^ᵇ⁺ 𝕓1ᵇ      ≡⟨⟩
            x              ≈⟨ sym (identityʳ x) ⟩
            x ∙ ε          ≡⟨⟩
            x ∙ x ^ⁱ 0     ≡⟨⟩
            x ^ⁱ 1         ≡⟨⟩
            x ^ⁱ ⟦ 𝕓1ᵇ ⇓⟧⁺ ∎
          loop⁺ x (b 0ᵇ) = begin
            x ^ᵇ⁺ (b 0ᵇ)    ≡⟨⟩
            (x ∙ x) ^ᵇ⁺ b   ≈⟨ even x b ⟩
            x ^ⁱ 2* ⟦ b ⇓⟧⁺ ≡⟨⟩
            x ^ⁱ ⟦ b 0ᵇ ⇓⟧⁺ ∎
          loop⁺ x (b 1ᵇ) = begin
            x ^ᵇ⁺ (b 1ᵇ)          ≡⟨⟩
            x ∙ (x ∙ x) ^ᵇ⁺ b     ≈⟨ ∙-congˡ (even x b) ⟩
            x ∙ x ^ⁱ 2* ⟦ b ⇓⟧⁺   ≡⟨⟩
            x ^ⁱ (1 + 2* ⟦ b ⇓⟧⁺) ≡⟨⟩
            x ^ⁱ ⟦ b 1ᵇ ⇓⟧⁺       ∎
\end{code}
This proof has more use that pure correctness. If we ever need to prove a
property of exponentials we can prove that fact with the inductive definition.
Then we can apply this proof to get a version of that property that works on
fast exponentiation for free.

\end{document}
